#include "totvs.ch"

#include "tlpp-core.th"

namespace dna.tech.DataSul2Protheus

using namespace dna.tech
using namespace dna.tech.DataSul2Protheus

#include "execin.th"
EXECIN CLASS SRFImport

static function SRFGetValue(xGet as variant,xDefault as variant) as variant
return(cacheData():Get("datasul2totvsprotheus",xGet,xDefault))

static function SRFSetValue(xVar as variant,xValue as variant) as variant
return(cacheData():Set("datasul2totvsprotheus",xVar,xValue))

static function SRFImportData(oMsNewProcess as object,lEnd as logical,lPercentage as logical) as logical

    local aAddIndex as array

    local bSourceSetFilter as codeblock

    local cAlias:="SRF" as character
    local cIndexKey as character
    local cTableImport:="SRF" as character

    local lSRFImport as logical

    local oTFIni:=cacheData():Get("datasul2totvsprotheus","readIniFile") as object
    local oTLogReport:=tLogReport():New() as object

    cIndexKey:=allTrim(oTFIni:GetPropertyValue(cTableImport,"IndexKey","RF_FILIAL+RF_MAT+DTOS(RF_DATABAS)+RF_PD"))
    aAddIndex:=ctools():StrTokCountArray(allTrim(oTFIni:GetPropertyValue(cTableImport,"AddIndex","RF_FILIAL,RF_MAT,RF_DATABAS,RF_PD")),",")

    bSourceSetFilter:={|jJSOnLine|SRFSourceFilter(jJSOnLine,oTFIni)}
    cacheData():Set("datasul2totvsprotheus",cTableImport+"SourceSetFilter",bSourceSetFilter)

    lSRFImport:=dna.tech.DataSul2Protheus.datasul2totvs():Execute("dna.tech.DataSul2Protheus.ImportData",@oMsNewProcess,@lEnd,lPercentage,@oTLogReport,@cAlias,@cIndexKey,@cTableImport,@aAddIndex)

    if (lSRFImport)
        ApMsgInfo(cAlias+" Table Import Completed","Info")
    else
        ApMsgInfo("Problems were encountered while processing the "+cTableImport+" table","Info")
    endif

    oTLogReport:PrintDialog("Log de Ocorrencias na Importacao: ["+cAlias+"]"+Upper(FWSX2Util():GetX2Name(cAlias,.F.)))
    MsAguarde({||oTLogReport:FreeObj()},"Releasing temporary files and workspace memory...","Wait...")
    FreeObj(@oTLogReport)

    FWFreeArray(@aAddIndex)

    DelClassIntF()

return(lSRFImport)

static function SRFSourceFilter(jJSOnLine as json,oTFIni as object) as logical

    local aLine:=jJSOnLine["uLine"] as array

    local lFilter:=.T. as logical

    local xFilter as variant

    begin sequence
        lFilter:=(Len(aLine)>=2)
        if (!lFilter)
            break
        endif
        xFilter:=strTran(aLine[1],'"',"")
        xFilter:=dna.tech.DataSul2Protheus.datasul2totvs():Execute("dna.tech.DataSul2Protheus.findInTable",oTFIni,"TabelaEmpresas",xFilter,.T.,.T.)
        lFilter:=(xFilter==cEmpAnt)
        if (!lFilter)
            break
        endif
        xFilter:=aLine[2]
        xFilter:=strTran(aLine[2],'"',"")
        xFilter:=dna.tech.DataSul2Protheus.datasul2totvs():Execute("dna.tech.DataSul2Protheus.findInTable",oTFIni,"TabelaFiliais",xFilter,.T.,.T.)
        lFilter:=(!empty(xFilter))
    end sequence

return(lFilter)
