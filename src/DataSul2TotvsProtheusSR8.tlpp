#include "totvs.ch"

#include "execin.th"
EXECIN CLASS SR8Import

static function SR8ImportData(oMsNewProcess as object,lEnd as logical) as logical

    local aAddIndex as array

    local cAlias:="SR8" as character
    local cIndexKey as character
    local cTableImport:="SR8" as character

    local lSR8Import as logical

    local oTFIni:=cacheData():Get("datasul2totvsprotheus","readIniFile") as object
    local oTLogReport:=tLogReport():New() as object

    cIndexKey:=allTrim(oTFIni:GetPropertyValue(cAlias,"IndexKey","R8_FILIAL+R8_MAT+DTOS(R8_DATAINI)+R8_TIPO"))
    aAddIndex:=strToKArr2(allTrim(oTFIni:GetPropertyValue(cAlias,"AddIndex","R8_FILIAL,R8_MAT,R8_DATAINI,R8_TIPO,R8_TIPOAFA")),",")

    lSR8Import:=datasul2totvs.Protheus():ImportData(@oMsNewProcess,@lEnd,@oTLogReport,@cAlias,@cIndexKey,@cTableImport,@aAddIndex)

    if (lSR8Import)
        ApMsgInfo(cAlias+" Table Import Completed","Info")
    else
        ApMsgInfo("Problems were encountered while processing the "+cTableImport+" table","Info")
    endif

    oTLogReport:PrintDialog("Log de Ocorrencias na Importacao: ["+cAlias+"]"+Upper(FWSX2Util():GetX2Name(cAlias,.F.)))
    oTLogReport:Clean()
    oTLogReport:=FreeObj(oTLogReport)

    FWFreeArray(@aAddIndex)

    if (type("oPVTHMSR8Seq")=="O")
        oPVTHMSR8Seq:Clean()
        oPVTHMSR8Seq:=FreeObj(oPVTHMSR8Seq)
    endif

    DelClassIntF()

return(lSR8Import)

static function getR8Seq()

    static c_stHMSR8SeqKey as character

    local cStack:="IMPORTDATA" as character

    local cR8MAT as character
    local cR8SEQ:="" as character
    local cR8TIPO as character
    local cR8FILIAL as character
    local cR8DATAINI as character
    local cR8TIPOAFA as character

    local cHMSR8SeqKey:="" as character

    local cTmpAlias as character

    local dR8DATAINI as date

    begin sequence

        if (type("cPVTSR8Alias")!="C")
            _SetNamedPRVT("cPVTSR8Alias",cacheData():Get("datasul2totvsprotheus","SR8ImportAlias","__NOALIAS__"),cStack)
        endif

        cTmpAlias:=cPVTSR8Alias
        if (select(cTmpAlias)==0)
            break
        endif

        cR8FILIAL:=(cTmpAlias)->R8_FILIAL
        cR8MAT:=(cTmpAlias)->R8_MAT
        dR8DATAINI:=(cTmpAlias)->R8_DATAINI
        cR8TIPO:=(cTmpAlias)->R8_TIPO
        cR8TIPOAFA:=(cTmpAlias)->R8_TIPOAFA

        cR8DATAINI:=DToS(dR8DATAINI)

        cHMSR8SeqKey:=&("cEmpAnt")
        cHMSR8SeqKey+=cR8FILIAL
        cHMSR8SeqKey+=cR8MAT

        if (type("oPVTHMSR8Seq")!="O")
            _SetNamedPRVT("oPVTHMSR8Seq",THashMap():New(),cStack)
        else
            if (c_stHMSR8SeqKey!=cHMSR8SeqKey)
                c_stHMSR8SeqKey:=cHMSR8SeqKey
                oPVTHMSR8Seq:Clean()
            endif
        endif

        cHMSR8SeqKey+=cR8DATAINI
        cHMSR8SeqKey+=cR8TIPO
        cHMSR8SeqKey+=cR8TIPOAFA

        if (!oPVTHMSR8Seq:Get(cHMSR8SeqKey,@cR8SEQ)).or.(Empty(cR8SEQ))
            if (type("nPVTSR8Seq")!="N")
                _SetNamedPRVT("nPVTSR8Seq",GetSx3Cache("R8_SEQ","X3_TAMANHO"),cStack)
                cR8SEQ:=Replicate("0",nPVTSR8Seq)
                _SetNamedPRVT("cPVTR8SEQ",cR8SEQ,cStack)
            endif
            cR8SEQ:=cPVTR8SEQ
        endif

        cR8SEQ:=__Soma1(cR8SEQ)

        oPVTHMSR8Seq:Set(cHMSR8SeqKey,cR8SEQ)

    end sequence

return(cR8SEQ)
