#define __NToS

#include "totvs.ch"
#include "dbstruct.ch"

#include "jsonhash.ch"
#include "jsonarray.ch"

static st_lfRACodUnic:=FindFunction("fRACodUnic") as logical
static st_oHMbTransform:=THashMap():New() as object

namespace datasul2totvs

class Protheus
    static method Activate() as logical
end class

static method Activate() class Protheus

    local bSRAMsNewProcess as codeblock
    local bSRFMsNewProcess as codeblock
    local bSR8MsNewProcess as codeblock
    
    local cGetFileMask as character
    local cGetFileTitle as character

    local lActivate as logical

    local nGetFileOptions as numeric

    local oDlg as object
    local oMenu as object
    local oFont as object
    local oFWDefSize as object
    
    local oSRAMSNewProcess as object
    local oSRFMSNewProcess as object
    local oSR8MSNewProcess as object

    if (valType(st_oHMbTransform)=="O")
        st_oHMbTransform:Clean()
    endif

    begin sequence

        cGetFileMask:="Arquivo de Configuração | DataSul2Protheus.ini"
        cGetFileTitle:="Arquivo de Configuração DataSul2Protheus.ini"
        nGetFileOptions:=(GETF_LOCALHARD+GETF_NETWORKDRIVE)
        cINIfile:=cGetFile(cGetFileMask,cGetFileTitle,1,".\",.F.,nGetFileOptions,/*[lArvore]*/,/*[lKeepCase]*/)

        MsAguarde({||lActivate:=readIniFile(@cINIfile)},"Loading Settings","Wait")
        if (!lActivate)
            break
        endif

        MsAguarde({||lActivate:=readTableDefinition()},"Loading structure of tables","Wait")
        if (!lActivate)
            break
        endif

        oFWDefSize:=FWDefSize():New(.T.,.T.)//Sem enchoicebar
        oFWDefSize:lLateral:=.F.//Calculo vertical
        oFWDefSize:AddObject("TMENU",100,100,.T.,.T.)//TMENU

        oFWDefSize:Process()

        nTop:=oFWDefSize:aWindSize[1]
        nLeft:=oFWDefSize:aWindSize[2]
        nBottom:=oFWDefSize:aWindSize[3]
        nRight:=oFWDefSize:aWindSize[4]

        oFont:=TFont():New("Courier New",nil,16,nil,.T.)

        bSRAMsNewProcess:={|lEnd|ProcRedefine(oSRAMSNewProcess,@oFont,0,650,650,.T.,.T.),ImportSRA(oSRAMSNewProcess,@lEnd)}
        bSRFMsNewProcess:={|lEnd|ProcRedefine(oSRFMSNewProcess,@oFont,0,650,650,.T.,.T.),ImportSRF(oSRFMSNewProcess,@lEnd)}
        bSR8MsNewProcess:={|lEnd|ProcRedefine(oSR8MSNewProcess,@oFont,0,650,650,.T.,.T.),ImportSR8(oSR8MSNewProcess,@lEnd)}

        DEFINE MSDIALOG oDlg TITLE "TOTVS :: DataSul To Protheus" FROM nTop,nLeft TO nBottom,nRight PIXEL OF getWNDDefault()

            MENU oMenu IMAGE "" OF oDlg
                MENUITEM "TOTVS :: DataSul To Protheus"
                MENU
                    MENUITEM "Import SRA" ACTION (oSRAMSNewProcess:=MsNewProcess():New(bSRAMsNewProcess,"Importing SRA :: ["+DToC(Date())+"]["+Time()+"]","Wait!",.T.),oSRAMSNewProcess:Activate())
                    MENUITEM "Import SRF" ACTION (oSRFMSNewProcess:=MsNewProcess():New(bSRFMsNewProcess,"Importing SRF :: ["+DToC(Date())+"]["+Time()+"]","Wait!",.T.),oSRFMSNewProcess:Activate())
                    MENUITEM "Import SR8" ACTION (oSR8MSNewProcess:=MsNewProcess():New(bSR8MsNewProcess,"Importing SR8 :: ["+DToC(Date())+"]["+Time()+"]","Wait!",.T.),oSR8MSNewProcess:Activate())
                ENDMENU
                MENU
                    MENUITEM "Exit" ACTION oDlg:End()
                ENDMENU
            ENDMENU

            oMenu:align:=CONTROL_ALIGN_LEFT
            oDlg:SetMenu(oMenu)

        ACTIVATE DIALOG oDlg CENTERED

        oFont:=FreeObj(oFont)
        oMenu:=FreeObj(oMenu)
        oDlg:=FreeObj(oDlg)

        oSRAMSNewProcess:=FreeObj(oSRAMSNewProcess)
        oSRFMSNewProcess:=FreeObj(oSRFMSNewProcess)
        oSR8MSNewProcess:=FreeObj(oSR8MSNewProcess)

    end sequence

    cacheData():delSection("datasul2totvs")

    if (valType(st_oHMbTransform)=="O")
        st_oHMbTransform:Clean()
    endif

return(lActivate)

static function readIniFile(cINIfile as character) as logical

    local cRootPath as character

    local cSPPDrive as character
    local cSPPPath as character
    local cSPPFile as character
    local cSPPExt as character

    local lreadIniFile:=.F. as logical

    local oTFIni as object

    begin sequence

        lreadIniFile:=(!empty(cINIfile).and.(File(cINIfile)))
        if (!lreadIniFile)
            ApMsgAlert("Configuration file invalid or not found","Attention")
            break
        endif

        SplitPath(cINIfile,@cSPPDrive,@cSPPPath,@cSPPFile,@cSPPExt)

        cRootPath:=cSPPDrive
        cRootPath+=cSPPPath

        cacheData():Set("datasul2totvs","cRootPath",cRootPath)

        oTFIni:=TFIni():New(cINIfile,"#")

        cacheData():Set("datasul2totvs","readIniFile",oTFIni)

    end sequence

return(lreadIniFile)

static function readTableDefinition() as logical

    local aFieldDef as array
    local aFieldDefTmp as array
    local aFieldsDef:=array(0) as array
    local aTablesImport as array

    local cLine as character
    local cLineUTF8 as character
    
    local cTable as character
    local cRootPath as character
    local cTablesDefinition as character
    local cFileTablesDefinition as character

    local nft_fRecNo as numeric
    local nft_fHandle as numeric

    local lSkipTable:=.F. as logical
    local lreadTableDefinition:=.F. as logical

    local oFT as object
    local oTFIni as object
    local oHMTablesImport as object

    begin sequence

        oTFIni:=cacheData():Get("datasul2totvs","readIniFile")

        aTablesImport:=aClone(oTFIni:GetAllProperties("TablesImport"))
        lreadTableDefinition:=(!empty(aTablesImport))
        if (!lreadTableDefinition)
            ApMsgAlert("Invalid import tables","Attention")
            break
        endif

        cTablesDefinition:=allTrim(oTFIni:GetPropertyValue("Tables","TablesDefinition",""))
        lreadTableDefinition:=(!empty(cTablesDefinition))
        if (!lreadTableDefinition)
            ApMsgAlert("Invalid TablesDefinition","Attention")
            break
        endif

        cRootPath:=cacheData():Get("datasul2totvs","cRootPath")
        cFileTablesDefinition:=(cRootPath+".."+cTablesDefinition)
        lreadTableDefinition:=file(cFileTablesDefinition)
        if (!lreadTableDefinition)
            ApMsgAlert("File ("+cFileTablesDefinition+") with the definitions of the tables not found","Attention")
            break
        endif

        oFT:=uft():New()

        nft_fHandle:=oFT:ft_fUse(cFileTablesDefinition)
        lreadTableDefinition:=(nft_fHandle>=0)
        if (!lreadTableDefinition)
            ApMsgAlert("Could not open the file: "+cFileTablesDefinition,"Attention")
            break
        endif

        oHMTablesImport:=aToHM(aTablesImport,2)

        oFT:ft_fGoTop()

        while (!oFT:ft_fEof())
            cLine:=oFT:ft_fReadLN()
            begin sequence
                if (empty(cLine))
                    break
                endif
                cTable:=""
                if ("Table:"==Left(cLine,6))
                    cTable:=strTran(cLine,"Table:","")
                    cTable:=allTrim(cTable)
                    lSkipTable:=(!oHMTablesImport:Get(cTable))
                    if (lSkipTable)
                        break
                    else
                        oFT:ft_fDelete()
                        oFT:ft_fSkip()
                    endif
                    aSize(aFieldsDef,0)
                    while (!oFT:ft_fEof())
                        cLine:=oFT:ft_fReadLN()
                        if (empty(cLine).or.("Field Name"==Left(cLine,10)).or.("-"==Left(cLine,1)))
                            oFT:ft_fDelete()
                            oFT:ft_fSkip()
                            loop
                        endif
                        if ("Table:"==Left(cLine,6))
                            cacheData():Set("datasul2totvs",cTable,aClone(aFieldsDef))
                            aSize(aFieldsDef,0)
                            nft_fRecNo:=oFT:ft_fRecNo()
                            oFT:ft_fGoTo(nft_fRecNo-1)
                            break
                        endif
                        while ("  "$cLine)
                            cLine:=strTran(cLine,"  "," ")
                        end while
                        cLineUTF8:=DecodeUTF8(cLine)
                        if (!empty(cLineUTF8))
                            cLine:=cLineUTF8
                        endif
                        aFieldDef:=strTokArr2(cLine," ")
                        cLine:=""
                        if (len(aFieldDef)==3)
                            aFieldDefTmp:=array(4)
                            aFieldDefTmp[1]:=aFieldDef[1]
                            aFieldDefTmp[2]:=aFieldDef[2]
                            aFieldDefTmp[3]:=""
                            aFieldDefTmp[4]:=aFieldDef[3]
                            aFieldDef:=aClone(aFieldDefTmp)
                            FWFreeArray(@aFieldDefTmp)
                        endif
                        aAdd(aFieldsDef,aClone(aFieldDef))
                        FWFreeArray(@aFieldDef)
                        oFT:ft_fDelete()
                        oFT:ft_fSkip()
                    end while
                    cacheData():Set("datasul2totvs",cTable,aClone(aFieldsDef))
                    aSize(aFieldsDef,0)
                elseif (lSkipTable)
                    break
                endif
            end sequence
            oFT:ft_fDelete()
            oFT:ft_fSkip()
        end while

        oFT:ft_fUse()
        oFT:=FreeObj(oFT)

    end sequence
    
    FWFreeArray(@aFieldsDef)

    if (valtype(oHMTablesImport)=="O")
        oHMTablesImport:Clean()
        oHMTablesImport:=FreeObj(oHMTablesImport)
    endif
    
    if (valtype(aTablesImport)=="A")
        FWFreeArray(@aTablesImport)
    endif

return(lreadTableDefinition)

static function getTargetFieldValue(oTFIni as object,aLine as array,cTargetField as character,aTargetField as array,aSourceFieldsDef as array,oHMSourceFieldsDef as object) as variant

    local aValueField as array
    local aSourceField as array

    local bTransform as codeblock

    local cType as character
    local cTransform as character
    local cSourceField as character

    local nSourceField as numeric
    local nSourceFields as numeric
    local nSourceFieldAT as numeric

    local xValue as variant

    cSourceField:=aTargetField[2]
    aSourceField:=strToKArr2(cSourceField,"+")
    nSourceFields:=len(aSourceField)
    aValueField:=Array(nSourceFields,2)
    for nSourceField:=1 to nSourceFields
        cSourceField:=aSourceField[nSourceField]
        if (!oHMSourceFieldsDef:Get(cSourceField,@cType))
            loop
        endif
        if (!oHMSourceFieldsDef:Get(cSourceField+"_AT",@nSourceFieldAT))
            loop
        endif
        if (cType=="char")
            xValue:=""
        endif
        aValueField[nSourceField][1]:=NormalizeData(aLine[nSourceFieldAT],cType)
        aValueField[nSourceField][2]:=cType
    next nSourceField

    FwFreeArray(@aSourceField)

    for nSourceField:=1 to nSourceFields
        if (aValueField[nSourceField][2]=="char")
            xValue+=aValueField[nSourceField][1]
        else
            xValue:=aValueField[nSourceField][1]
        endif
    next nSourceField

    FWFreeArray(@aValueField)

    if (!st_oHMbTransform:Get(cTargetField,@bTransform))
        cTransform:=allTrim(oTFIni:GetPropertyValue(cTargetField,"Transform",""))
        if (cTransform=="FindInTable")
            cTransform:=allTrim(oTFIni:GetPropertyValue(cTargetField,cTransform,""))
            bTransform:=&(cTransform)
        elseif (!empty(cTransform))
            bTransform:=&(cTransform)
        endif
        st_oHMbTransform:Set(cTargetField,bTransform)
    endif

    if (valtype(bTransform)=="B")
        xValue:=Eval(bTransform,xValue,oTFIni)
    endif

return(xValue)

static function NormalizeData(xValue as variant,cType as character) as variant

    if (cType=="char")
       xValue:=strTran(xValue,'"',"")
    elseif (cType=="inte")
        xValue:=Int(val(xValue))
    elseif ("deci"$cType)
        if (","$xValue)
            xValue:=strTran(xValue,",",".")
        endif
        xValue:=val(xValue)
    elseif (cType=="date")
        xValue:=CToD(xValue)
    elseif (cType=="logi")
        xValue:=(Left(lower(xValue),1)$"sty1")
    endif

return(xValue)

static function FindInTable(oTFIni as object,cTable as character,xValue as variant,lTableInFile as logical)

    local cType as character

    local cINIfile as character
    local cRootPath as character

    local oTFIniTable as object

    local xTmp as variant

    cType:=valType(xValue)
    if (cType=="N")
        xValue:=ctools():NToS(xValue)
    elseif (cType!="C")
        xValue:=cValToChar(xValue)
    endif

    if (oTFIni:ExistSection(cTable))
        if (oTFIni:GetATProperty(cTable,"FindInTableFile")>0)
            dfv():Default(@lTableInFile,.F.)
            if (lTableInFile)
                oTFIniTable:=cacheData():Get("datasul2totvs",cTable,nil)
                if (oTFIniTable==nil)
                    cRootPath:=cacheData():Get("datasul2totvs","cRootPath")
                    cINIfile:=cRootPath
                    cINIfile+=oTFIni:GetPropertyValue(cTable,"FindInTableFile","")
                    oTFIniTable:=TFIni():New(cINIfile,"#")
                    cacheData():Set("datasul2totvs",cTable,oTFIniTable)
                endif
                xTmp:=oTFIniTable:GetPropertyValue(cTable,xValue,"")
                if ((empty(xTmp)).and.((oTFIniTable:GetATProperty(cTable,"__DFV__")>0)))
                    xTmp:=oTFIniTable:GetPropertyValue(cTable,"__DFV__","")
                endif
                xValue:=xTmp
            else
                oTFIniTable:=cacheData():Get("datasul2totvs",cTable,nil)
                if (oTFIniTable==nil)
                    cRootPath:=cacheData():Get("datasul2totvs","cRootPath")
                    cINIfile:=cRootPath
                    cINIfile+=oTFIni:GetPropertyValue(cTable,"FindInTableFile","")
                    oTFIniTable:=TFIni():New(cINIfile,"#")
                    cacheData():Set("datasul2totvs",cTable,oTFIniTable)
                endif
                xValue:=FindInTable(oTFIniTable,cTable,xValue)
            endif
        elseif ((oTFIni:GetATProperty(cTable,xValue)>0))
            xValue:=oTFIni:GetPropertyValue(cTable,xValue,"")
        elseif ((oTFIni:GetATProperty(cTable,"__DFV__")>0))
            xValue:=oTFIni:GetPropertyValue(cTable,"__DFV__","")
        endif
    endif

return(xValue)

static function getR8Seq() as character

    static c_stHMSR8SeqKey as character

    local cStack:="DATASUL2TOTVS.IMPORTSR8" as character

    local cR8MAT as character
    local cR8SEQ:="" as character
    local cR8TIPO as character
    local cR8FILIAL as character
    local cR8DATAINI as character
    local cR8TIPOAFA as character

    local cHMSR8SeqKey:="" as character

    local cTmpAlias as character

    local dR8DATAINI as date

    begin sequence

        if (type("cPVTSR8Alias")!="C")
            _SetNamedPRVT("cPVTSR8Alias",cacheData():Get("datasul2totvs","importSR8Alias","__NOALIAS__"),cStack)
        endif

        cTmpAlias:=cPVTSR8Alias
        if (select(cTmpAlias)==0)
            break
        endif

        cR8FILIAL:=(cTmpAlias)->R8_FILIAL
        cR8MAT:=(cTmpAlias)->R8_MAT
        dR8DATAINI:=(cTmpAlias)->R8_DATAINI
        cR8TIPO:=(cTmpAlias)->R8_TIPO
        cR8TIPOAFA:=(cTmpAlias)->R8_TIPOAFA

        cR8DATAINI:=DToS(dR8DATAINI)

        cHMSR8SeqKey:=&("cEmpAnt")
        cHMSR8SeqKey+=cR8FILIAL
        cHMSR8SeqKey+=cR8MAT

        if (type("oPVTHMSR8Seq")!="O")
            _SetNamedPRVT("oPVTHMSR8Seq",THashMap():New(),cStack)
        else
            if (c_stHMSR8SeqKey!=cHMSR8SeqKey)
                c_stHMSR8SeqKey:=cHMSR8SeqKey
                oPVTHMSR8Seq:Clean()
            endif
        endif

        cHMSR8SeqKey+=cR8DATAINI
        cHMSR8SeqKey+=cR8TIPO
        cHMSR8SeqKey+=cR8TIPOAFA

        if (!oPVTHMSR8Seq:Get(cHMSR8SeqKey,@cR8SEQ)).or.(Empty(cR8SEQ))
            if (type("nPVTSR8Seq")!="N")
                _SetNamedPRVT("nPVTSR8Seq",GetSx3Cache("R8_SEQ","X3_TAMANHO"),cStack)
                cR8SEQ:=Replicate("0",nPVTSR8Seq)
                _SetNamedPRVT("cPVTR8SEQ",cR8SEQ,cStack)
            endif
            cR8SEQ:=cPVTR8SEQ
        endif

        cR8SEQ:=__Soma1(cR8SEQ)

        oPVTHMSR8Seq:Set(cHMSR8SeqKey,cR8SEQ)

    end sequence        

return(cR8SEQ)

static function ProcRedefine(oProcess as object,oFont as object,nLeft as numeric,nWidth as numeric,nCTLFLeft as numeric,lODlgF as logical,lODlgW as logical) as logical
    local lProcRedefine as logical
    lProcRedefine:=MsProcess():ProcRedefine(@oProcess,@oFont,@nLeft,@nWidth,@nCTLFLeft,@lODlgF,@lODlgW)
    return(lProcRedefine)

static function ImportData(oMsNewProcess as object,lEnd as logical,aLog as array,aLogDet as array,aLogTitle as array,cAlias as character,cIndexKey as character,cTableImport as character,aAddIndex as array) as logical

    local aLine as array
    local aField as array
    
    local aFieldsDef:=array(0) as array
    
    local aTargetStruct:=(cAlias)->(dbStruct()) as array
    local aTargetFieldsDef as array

    local cField as character
    local cLineUTF8 as character

    local cTmpAlias:=getNextAlias() as character
    
    local cLine as character
    local cRootPath as character
    local cDataPath as character
    local cSourceFile as character
    local cSourceTable as character

    local cRecCount as character

    local cIndexID as character
    local cFieldFilial as character

    local cIncProc as character
    local cTimeRemaining as character

    local lFound as logical
    local lAddNew as logical
    local lAppend as logical
    local ldbCommit as logical
    local lImportData as logical
    local lRecCountStep as logical

    local nField as numeric
    local nFields as numeric

    local nRecProc:=0 as numeric
    local nRecCount as numeric
    local nRecCountStep as numeric
    local nCommitBuffer as numeric
    local nTargetFieldPos as numeric

    local nIndexKey:=retOrder(cAlias,cIndexKey) as numeric

    local nIncProcStep as numeric

    local nft_fRecNo as numeric
    local nft_fHandle as numeric

    local oFT as object
    local oTFIni as object
    local oProgress as object
    local oHMTargetStruct as object
    local oTimeRemaining as object
    local oFWTemporaryTable as object
    local oHMSourceFieldsDef as object

    begin sequence

        oTFIni:=cacheData():Get("datasul2totvs","readIniFile")
        
        cSourceTable:=allTrim(oTFIni:GetPropertyValue("TablesImport",cTableImport,""))
        lImportData:=(!empty(cSourceTable))
        if (!lImportData)
            ApMsgAlert("There is no setting for the SRA table","Attention")
            break
        endif

        cDataPath:=allTrim(oTFIni:GetPropertyValue("Tables","TablesData",""))
        lImportData:=(!empty(cDataPath))
        if (!lImportData)
            ApMsgAlert("Invalida Data Path ("+cDataPath+")","Attention")
            break
        endif

        cRootPath:=cacheData():Get("datasul2totvs","cRootPath")
        cSourceFile:=(cRootPath+".."+cDataPath+cSourceTable+".csv")
        lImportData:=file(cSourceFile)
        if (!lImportData)
            ApMsgAlert("File ("+cSourceFile+") not found","Attention")
            break
        endif

        aFieldsDef:=aClone(cacheData():Get("datasul2totvs",cSourceTable,aFieldsDef))
        lImportData:=(!empty(aFieldsDef))
        if (!lImportData)
            ApMsgAlert("Field definitions for the "+cSourceTable+" table not found","Attention")
            break
        endif

        oHMSourceFieldsDef:=THashMap():New()
        nFields:=Len(aFieldsDef)
        for nField:=1 to nFields
            oHMSourceFieldsDef:Set(aFieldsDef[nField][1],aFieldsDef[nField][2])
            oHMSourceFieldsDef:Set(aFieldsDef[nField][1]+"_AT",nField)
        next nField

        aTargetFieldsDef:=aClone(oTFIni:GetAllProperties(cSourceTable))
        lImportData:=(!empty(aFieldsDef))
        if (!lImportData)
            ApMsgAlert("Field definitions for the "+cTableImport+" table not found","Attention")
            break
        endif

        nFields:=Len(aTargetStruct)
        for nField:=1 to nFields
            aAdd(aTargetStruct[nField],nField)
        next nField
        oHMTargetStruct:=aToHM(aTargetStruct,DBS_NAME)

        nFields:=Len(aTargetFieldsDef)
        for nField:=1 to nFields
            cField:=aTargetFieldsDef[nField][1]
            if (oHMTargetStruct:Get(cField,@aField))
                nTargetFieldPos:=aField[1][DBS_ALEN+1]
            else
                nTargetFieldPos:=0
            endif
            aAdd(aTargetFieldsDef[nField],nTargetFieldPos)
        next nField

        oHMTargetStruct:Clean()
        oHMTargetStruct:=FreeObj(oHMTargetStruct)

        oFT:=uft():New()

        MsAguarde({||nft_fHandle:=oFT:ft_fUse(cSourceFile)},"Loading Data File","Wait")
        lImportData:=(nft_fHandle>=0)
        if (!lImportData)
            ApMsgAlert("Could not open the file: "+cSourceFile,"Attention")
            break
        endif

        oFWTemporaryTable:=FWTemporaryTable():New(@cTmpAlias,@aTargetStruct)
        oFWTemporaryTable:AddIndex("01",aAddIndex)
        oFWTemporaryTable:Create()

        cacheData():Set("datasul2totvs","import"+cTableImport+"Alias",cTmpAlias)
        cacheData():Set("datasul2totvs","import"+cTableImport+"Table",oFWTemporaryTable:getRealName())

        FWFreeArray(@aField)
        FWFreeArray(@aTargetStruct)

        cFieldFilial:=PrefixoCpo(cAlias)
        cFieldFilial+="_FILIAL"

        nIncProcStep:=Int(val(allTrim(oTFIni:GetPropertyValue("General","IncProcStep","500"))))

        begin sequence

            nRecCount:=oFT:ft_fLastRec()
            cRecCount:=cTools():NToS(nRecCount)
            nRecCountStep:=if(nRecCount>=(nIncProcStep*2),Int(nRecCount/nIncProcStep),nRecCount)
            lRecCountStep:=(nRecCountStep!=nRecCount)
            if (lRecCountStep)
                nRecCountStep+=(nRecCount-(nRecCountStep*nIncProcStep))
            endif
            oMsNewProcess:SetRegua1(nRecCountStep)
            oMsNewProcess:SetRegua2(0)
            oTimeRemaining:=timeRemaining():New(nRecCountStep)

            oProgress:=txtProgress():New()

            TCSrvMap(cTmpAlias,nil,.F.)

            oFT:ft_fGoTop()
            while (!oFT:ft_fEof())
                nft_fRecNo:=oFT:ft_fRecNo()
                cLine:=oFT:ft_fReadLN()
                oFT:ft_fDelete()
                if (empty(cLine))
                    oFT:ft_fSkip()
                    loop
                endif
                cLineUTF8:=DecodeUTF8(cLine)
                if (empty(cLineUTF8))
                    aAdd(aLogTitle,"foram Encontradas inconsistencias na linha ["+cTools():NToS(nft_fRecNo)+"]")
                    aAdd(aLog,cLine)
                    aAdd(aLog,"Existem Caracteres Invalidos")
                    aAdd(aLogDet,aClone(aLog))
                    aSize(aLog,0)
                else
                    cLine:=cLineUTF8
                endif
                aLine:=strTokArr2(cLine,";")
                cLine:=""
                lAppend:=.T.
                for nField:=1 to nFields
                    nTargetFieldPos:=aTargetFieldsDef[nField][Len(aTargetFieldsDef[nField])]
                    if (nTargetFieldPos==0)
                        loop
                    endif
                    cField:=aTargetFieldsDef[nField][1]
                    if (cField==cFieldFilial)
                        xValue:=(cTmpAlias)->(getTargetFieldValue(@oTFIni,@aLine,@cField,aTargetFieldsDef[nField],@aFieldsDef,@oHMSourceFieldsDef))
                        if (empty(xValue))
                            lAppend:=.F.
                            exit
                        endif
                    endif
                next nField
                if (lAppend)
                    (cTmpAlias)->(dbAppend(.F.))
                        for nField:=1 to nFields
                            nTargetFieldPos:=aTargetFieldsDef[nField][Len(aTargetFieldsDef[nField])]
                            if (nTargetFieldPos==0)
                                loop
                            endif
                            cField:=aTargetFieldsDef[nField][1]
                            xValue:=(cTmpAlias)->(getTargetFieldValue(@oTFIni,@aLine,@cField,aTargetFieldsDef[nField],@aFieldsDef,@oHMSourceFieldsDef))
                            if (empty(xValue))
                                loop
                            endif
                            (cTmpAlias)->(FieldPut(nTargetFieldPos,xValue))
                        next nField
                    ldbCommit:=(++nCommitBuffer>=1024)
                endif
                FWFreeArray(@aLine)
                nRecProc++
                if ((!lRecCountStep).or.(Mod(nRecProc,nIncProcStep)==0).or.(nRecProc>=nRecCount))
                    oTimeRemaining:Calcule(.T.)
                    cTimeRemaining:=" :: End of processing in ["+oTimeRemaining:GetcTRemaining()+"]["+DtoC(oTimeRemaining:GetdEndTime())+"]"
                    cTimeRemaining+="["+oTimeRemaining:GetcEndTime()+"]"
                    cTimeRemaining+="["+oTimeRemaining:GetcAverageTime()+"]"
                    cIncProc:="["+oProgress:Eval()+"] "
                    cIncProc+="Processing Record: "
                    cIncProc+=cTools():NToS(nft_fRecNo)
                    cIncProc+="/"
                    cIncProc+=cRecCount
                    cIncProc+=cTimeRemaining
                    cIncProc+="::["+cTools():NToS(if((nRecProc>=nRecCount),100,nRecProc/nRecCountStep))+"%]"
                    oMsNewProcess:IncRegua1(cIncProc)
                endif
                lEnd:=oMsNewProcess:lEnd
                if (lEnd)
                    aAdd(aLogTitle,"Process canceled by user")
                    aAdd(aLog,"Process canceled by user ["+DtoC(Date())+"]["+Time()+"]")
                    aAdd(aLogDet,aClone(aLog))
                    aSize(aLog,0)
                    exit
                endif
                if (ldbCommit)
                    nCommitBuffer:=0
                    (cTmpAlias)->(dbCommitAll())
                endif
                oFT:ft_fSkip()
            end while

            if (nCommitBuffer>0)
                (cTmpAlias)->(dbCommitAll())
            endif

            oFT:ft_fUse()

            if (lEnd)
                break
            endif

            nRecCount:=(cTmpAlias)->(recCount())
            cRecCount:=cTools():NToS(nRecCount)
            nRecCountStep:=if(nRecCount>=(nIncProcStep*2),Int(nRecCount/nIncProcStep),nRecCount)
            lRecCountStep:=(nRecCountStep!=nRecCount)
            if (lRecCountStep)
                nRecCountStep+=(nRecCount-(nRecCountStep/nIncProcStep))
            endif
            oMsNewProcess:SetRegua2(nRecCountStep)
            oTimeRemaining:SetRemaining(nRecCountStep)

            (cAlias)->(dbSetOrder(nIndexKey))

            nRecProc:=0

            (cTmpAlias)->(dbGoTop())
            while (cTmpAlias)->(!eof())
                cIndexID:=(cTmpAlias)->(&(cIndexKey))
                lFound:=(cAlias)->(dbSeek(cIndexID,.F.))
                lAddNew:=(!lFound)
                if ((cAlias)->(recLock(cAlias,lAddNew)))
                    for nField:=1 to nFields
                        nTargetFieldPos:=aTargetFieldsDef[nField][Len(aTargetFieldsDef[nField])]
                        if (nTargetFieldPos==0)
                            loop
                        endif
                        xValue:=(cTmpAlias)->(FieldGet(nTargetFieldPos))
                        if (empty(xValue))
                            loop
                        endif
                        (cAlias)->(FieldPut(nTargetFieldPos,xValue))
                    next nField
                    if (cAlias=="SRA")
                        if (empty((cAlias)->RA_CODUNIC))
                            if (st_lfRACodUnic)
                                (cAlias)->RA_CODUNIC:=fRACodUnic()
                            endif
                        endif
                    endif
                    (cAlias)->(msUnLock())
                endif
                nRecProc++
                if ((!lRecCountStep).or.(Mod(nRecProc,100)==0).or.(nRecProc>=nRecCount))
                    oTimeRemaining:Calcule(.T.)
                    cTimeRemaining:=" :: End of processing in ["+oTimeRemaining:GetcTRemaining()+"]["+DtoC(oTimeRemaining:GetdEndTime())+"]"
                    cTimeRemaining+="["+oTimeRemaining:GetcEndTime()+"]"
                    cTimeRemaining+="["+oTimeRemaining:GetcAverageTime()+"]"
                    cIncProc:="["+oProgress:Eval()+"] "
                    cIncProc+="Updating Record: "
                    cIncProc+=cTools():NToS(nRecProc)
                    cIncProc+="/"
                    cIncProc+=cRecCount
                    cIncProc+=cTimeRemaining
                    cIncProc+="::["+cTools():NToS(if((nRecProc>=nRecCount),100,nRecProc/nRecCountStep))+"%]"
                    oMsNewProcess:IncRegua2(cIncProc)
                endif                    
                lEnd:=oMsNewProcess:lEnd
                if (lEnd)
                    aAdd(aLogTitle,"Process canceled by user")
                    aAdd(aLog,"Process canceled by user ["+DtoC(Date())+"]["+Time()+"]")
                    aAdd(aLogDet,aClone(aLog))
                    aSize(aLog,0)
                    break
                endif
                (cTmpAlias)->(dbSkip())
            end while

        end sequence

        (cTmpAlias)->(dbCloseArea())

        oFWTemporaryTable:Delete()
        oFWTemporaryTable:=FreeObj(oFWTemporaryTable)

    end sequence

    if (valtype(oHMSourceFieldsDef)=="O")
        oHMSourceFieldsDef:Clean()
        oHMSourceFieldsDef:=FreeObj(oHMSourceFieldsDef)
    endif

    if (valType(aTargetStruct)=="A")
        FWFreeArray(@aTargetStruct)
    endif

    if (valType(aFieldsDef)=="A")
        FWFreeArray(@aFieldsDef)
    endif

    if (valType(aTargetFieldsDef)=="A")
        FWFreeArray(@aTargetFieldsDef)
    endif

return(lImportData)

static function importSRA(oMsNewProcess as object,lEnd as logical) as logical

    local aLog:=array(0) as array
    local aLogDet:=array(0) as array
    local aLogTitle:=array(0) as array

    local lImportSRA as logical

    lImportSRA:=ImportData(@oMsNewProcess,@lEnd,@aLog,@aLogDet,@aLogTitle,"SRA","RA_FILIAL+RA_MAT","SRA",{"RA_FILIAL","RA_MAT"})

    if (lImportSRA)
        lImportSRA:=ImportData(@oMsNewProcess,@lEnd,@aLog,@aLogDet,@aLogTitle,"SRA","RA_FILIAL+RA_MATMIG","SRA_RHPF",{"RA_FILIAL","RA_MATMIG"})
        if (lImportSRA)
            ApMsgInfo("SRA Table Import Completed","Info")
        else
            ApMsgInfo("Problems were encountered while processing the SRA_RHPF table","Info")
        endif
    endif

    if (!empty(aLogDet))
        fMakeLog(aLogDet,aLogTitle,nil,nil,ProcName(),"Log de Ocorrencias na Importacao","G","L",nil,.F.)
    endif

    FWFreeArray(@aLog)
    FWFreeArray(@aLogDet)
    FWFreeArray(@aLogTitle)

return(lImportSRA)

static function importSRF(oMsNewProcess as object,lEnd as logical) as logical

    local aLog:=array(0) as array
    local aLogDet:=array(0) as array
    local aLogTitle:=array(0) as array

    local lImportSRF as logical

    lImportSRF:=ImportData(@oMsNewProcess,@lEnd,@aLog,@aLogDet,@aLogTitle,"SRF","RF_FILIAL+RF_MAT+DTOS(RF_DATABAS)+RF_PD","SRF",{"RF_FILIAL","RF_MAT","RF_DATABAS","RF_PD"})

    if (lImportSRF)
        ApMsgInfo("SRF Table Import Completed","Info")
    endif

    if (!empty(aLogDet))
        fMakeLog(aLogDet,aLogTitle,nil,nil,ProcName(),"Log de Ocorrencias na Importacao","G","L",nil,.F.)
    endif

    FWFreeArray(@aLog)
    FWFreeArray(@aLogDet)
    FWFreeArray(@aLogTitle)

return(lImportSRF)

static function importSR8(oMsNewProcess as object,lEnd as logical) as logical

    local aLog:=array(0) as array
    local aLogDet:=array(0) as array
    local aLogTitle:=array(0) as array

    local lImportSR8 as logical

    lImportSR8:=ImportData(@oMsNewProcess,@lEnd,@aLog,@aLogDet,@aLogTitle,"SR8","R8_FILIAL+R8_MAT+DTOS(R8_DATAINI)+R8_TIPO","SR8",{"R8_FILIAL","R8_MAT","R8_DATAINI","R8_TIPO","R8_TIPOAFA"})

    if (lImportSR8)
        ApMsgInfo("SR8 Table Import Completed","Info")
    endif

    if (!empty(aLogDet))
        fMakeLog(aLogDet,aLogTitle,nil,nil,ProcName(),"Log de Ocorrencias na Importacao","G","L",nil,.F.)
    endif

    FWFreeArray(@aLog)
    FWFreeArray(@aLogDet)
    FWFreeArray(@aLogTitle)

return(lImportSR8)
