#define __NToS

#DEFINE DTS_DBS_NAME 1
#DEFINE DTS_DBS_TYPE 2
#DEFINE DTS_DBS_FLG 3
#DEFINE DTS_DBS_FORMAT 4

#DEFINE DTS_DBS_ALEN 4

#include "totvs.ch"
#include "shell.ch"

#include "dbstruct.ch"

#include "jsonhash.ch"
#include "jsonarray.ch"

namespace datasul2totvs

class Protheus
    static method Activate() as logical
end class

static method Activate() class Protheus

    local bSRJMsNewProcess as codeblock
    local bSR6MsNewProcess as codeblock
    local bSRVMsNewProcess as codeblock
    local bRCEMsNewProcess as codeblock
    local bSQBMsNewProcess as codeblock

    local bSRAMsNewProcess as codeblock
    local bSRBMsNewProcess as codeblock
    local bSRDMsNewProcess as codeblock
    local bSRFMsNewProcess as codeblock
    local bSRQMsNewProcess as codeblock
    local bSR3MsNewProcess as codeblock
    local bSR8MsNewProcess as codeblock
    local bSP8MsNewProcess as codeblock

    local cGetFileRoot:="\" as character
    local cGetFileMask as character
    local cGetFileTitle as character

    local lActivate as logical
    local lSetCentury:=__SetCentury("ON") as logical
    local lSendDataToServer as logical

    local nGetFileOptions as numeric

    local oDlg as object
    local oMenu as object
    local oFont as object
    local oFWDefSize as object

    local oMSNewProcess as object

    private st_oHMbTransform:=THashMap():New() as object

    private cCadastro:="TOTVS :: DataSul To Protheus" as character

    begin sequence

        lActivate:=((FWIsAdmin(__cUserID)).and.(GetNewPar("TOTVSDS2PT",.T.)))
        if (!lActivate)
            ApMsgAlert("User without administrative privileges")
            break
        endif

        lSendDataToServer:=((getComputerName()$(GetNewPar("TOTVSD2PCN","DNA-CSDJ-01"))).and.(ApMsgNoYes("Sending Data To Server?")))
        if (lSendDataToServer)
            MsAguarde({||lSendDataToServer:=sendDataToServer(@cGetFileRoot,.F.)},"Sending Data To Server","Wait!")
        elseif (file("\datasul2Protheus\ini\DataSul2Protheus.ini"))
            cGetFileRoot:="SERVIDOR\datasul2protheus\ini\"
        endif

        cGetFileMask:="Arquivo de Configuração | DataSul2Protheus.ini"
        cGetFileTitle:="Arquivo de Configuração DataSul2Protheus.ini"
        nGetFileOptions:=(GETF_LOCALHARD+GETF_NETWORKDRIVE)
        cINIFile:=cGetFile(cGetFileMask,cGetFileTitle,1,cGetFileRoot,.F.,nGetFileOptions,/*[lArvore]*/,/*[lKeepCase]*/)

        lActivate:=file(cINIFile)
        if (!lActivate)
            ApMsgAlert("Configuration file invalid ["+cINIFile+"] or not found","Attention")
            break
        endif

        lActivate:=("datasul2protheus.ini"$lower(cINIFile))
        if (!lActivate)
            ApMsgAlert("Configuration file invalid ["+cINIFile+"] or not found","Attention")
            break
        endif

        MsAguarde({||lActivate:=readIniFile(@cINIFile)},"Loading Settings","Wait")
        if (!lActivate)
            break
        endif

        MsAguarde({||lActivate:=readTableDefinition()},"Loading structure of tables","Wait")
        if (!lActivate)
            break
        endif

        oFWDefSize:=FWDefSize():New(.T.,.T.)//Sem enchoicebar
        oFWDefSize:lLateral:=.F.//Calculo vertical
        oFWDefSize:AddObject("TMENU",100,100,.T.,.T.)//TMENU

        oFWDefSize:Process()

        nTop:=oFWDefSize:aWindSize[1]
        nLeft:=oFWDefSize:aWindSize[2]
        nBottom:=oFWDefSize:aWindSize[3]
        nRight:=oFWDefSize:aWindSize[4]

        oFont:=TFont():New("Courier New",nil,16,nil,.T.)

        bSRJMsNewProcess:={|lEnd|ProcRedefine(oMSNewProcess,@oFont,0,650,650,.T.,.T.),ImportSRJ():ImportData(oMSNewProcess,@lEnd)}
        bSR6MsNewProcess:={|lEnd|ProcRedefine(oMSNewProcess,@oFont,0,650,650,.T.,.T.),ImportSR6():ImportData(oMSNewProcess,@lEnd)}
        bSRVMsNewProcess:={|lEnd|ProcRedefine(oMSNewProcess,@oFont,0,650,650,.T.,.T.),ImportSRV():ImportData(oMSNewProcess,@lEnd)}
        bRCEMsNewProcess:={|lEnd|ProcRedefine(oMSNewProcess,@oFont,0,650,650,.T.,.T.),ImportRCE():ImportData(oMSNewProcess,@lEnd)}
        bSQBMsNewProcess:={|lEnd|ProcRedefine(oMSNewProcess,@oFont,0,650,650,.T.,.T.),ImportSQB():ImportData(oMSNewProcess,@lEnd)}

        bSRAMsNewProcess:={|lEnd|ProcRedefine(oMSNewProcess,@oFont,0,650,650,.T.,.T.),ImportSRA():ImportData(oMSNewProcess,@lEnd)}
        bSRBMsNewProcess:={|lEnd|ProcRedefine(oMSNewProcess,@oFont,0,650,650,.T.,.T.),ImportSRB():ImportData(oMSNewProcess,@lEnd)}
        bSRDMsNewProcess:={|lEnd|ProcRedefine(oMSNewProcess,@oFont,0,650,650,.T.,.T.),ImportSRD():ImportData(oMSNewProcess,@lEnd)}
        bSRFMsNewProcess:={|lEnd|ProcRedefine(oMSNewProcess,@oFont,0,650,650,.T.,.T.),ImportSRF():ImportData(oMSNewProcess,@lEnd)}
        bSRQMsNewProcess:={|lEnd|ProcRedefine(oMSNewProcess,@oFont,0,650,650,.T.,.T.),ImportSRQ():ImportData(oMSNewProcess,@lEnd)}
        bSR3MsNewProcess:={|lEnd|ProcRedefine(oMSNewProcess,@oFont,0,650,650,.T.,.T.),ImportSR3():ImportData(oMSNewProcess,@lEnd)}
        bSR8MsNewProcess:={|lEnd|ProcRedefine(oMSNewProcess,@oFont,0,650,650,.T.,.T.),ImportSR8():ImportData(oMSNewProcess,@lEnd)}
        bSP8MsNewProcess:={|lEnd|ProcRedefine(oMSNewProcess,@oFont,0,650,650,.T.,.T.),ImportSP8():ImportData(oMSNewProcess,@lEnd)}

        DEFINE MSDIALOG oDlg TITLE cCadastro FROM nTop,nLeft TO nBottom,nRight PIXEL OF getWNDDefault()

            MENU oMenu IMAGE "" OF oDlg
                MENUITEM cCadastro
                MENU 
                    MENUITEM "Registers"
                    MENU
                        MENUITEM "SRJ :: "+Upper(FWSX2Util():GetX2Name("SRJ",.F.)) ACTION (oMSNewProcess:=MsNewProcess():New(bSRJMsNewProcess,"Importing SRJ :: "+Upper(FWSX2Util():GetX2Name("SRJ",.F.))+" :: ["+DToC(Date())+"]["+Time()+"]","Wait!",.T.),oMSNewProcess:Activate())
                        MENUITEM "SR6 :: "+Upper(FWSX2Util():GetX2Name("SR6",.F.)) ACTION (oMSNewProcess:=MsNewProcess():New(bSR6MsNewProcess,"Importing SR6 :: "+Upper(FWSX2Util():GetX2Name("SR6",.F.))+" :: ["+DToC(Date())+"]["+Time()+"]","Wait!",.T.),oMSNewProcess:Activate())
                        MENUITEM "SRV :: "+Upper(FWSX2Util():GetX2Name("SRV",.F.)) ACTION (oMSNewProcess:=MsNewProcess():New(bSRVMsNewProcess,"Importing SRV :: "+Upper(FWSX2Util():GetX2Name("SRV",.F.))+" :: ["+DToC(Date())+"]["+Time()+"]","Wait!",.T.),oMSNewProcess:Activate())
                        MENUITEM "RCE :: "+Upper(FWSX2Util():GetX2Name("RCE",.F.)) ACTION (oMSNewProcess:=MsNewProcess():New(bRCEMsNewProcess,"Importing RCE :: "+Upper(FWSX2Util():GetX2Name("RCE",.F.))+" :: ["+DToC(Date())+"]["+Time()+"]","Wait!",.T.),oMSNewProcess:Activate())
                        MENUITEM "SQB :: "+Upper(FWSX2Util():GetX2Name("SQB",.F.)) ACTION (oMSNewProcess:=MsNewProcess():New(bSQBMsNewProcess,"Importing SQB :: "+Upper(FWSX2Util():GetX2Name("SQB",.F.))+" :: ["+DToC(Date())+"]["+Time()+"]","Wait!",.T.),oMSNewProcess:Activate())
                    ENDMENU
                    MENUITEM "Employees"    
                    MENU
                        MENUITEM "SRA :: "+Upper(FWSX2Util():GetX2Name("SRA",.F.)) ACTION (oMSNewProcess:=MsNewProcess():New(bSRAMsNewProcess,"Importing SRA :: "+Upper(FWSX2Util():GetX2Name("SRA",.F.))+" :: ["+DToC(Date())+"]["+Time()+"]","Wait!",.T.),oMSNewProcess:Activate())
                        MENUITEM "SRQ :: "+Upper(FWSX2Util():GetX2Name("SRQ",.F.)) ACTION (oMSNewProcess:=MsNewProcess():New(bSRQMsNewProcess,"Importing SRQ :: "+Upper(FWSX2Util():GetX2Name("SRQ",.F.))+" :: ["+DToC(Date())+"]["+Time()+"]","Wait!",.T.),oMSNewProcess:Activate())
                        MENUITEM "SRB :: "+Upper(FWSX2Util():GetX2Name("SRB",.F.)) ACTION (oMSNewProcess:=MsNewProcess():New(bSRBMsNewProcess,"Importing SRB :: "+Upper(FWSX2Util():GetX2Name("SRB",.F.))+" :: ["+DToC(Date())+"]["+Time()+"]","Wait!",.T.),oMSNewProcess:Activate())
                        MENUITEM "SRF :: "+Upper(FWSX2Util():GetX2Name("SRF",.F.)) ACTION (oMSNewProcess:=MsNewProcess():New(bSRFMsNewProcess,"Importing SRF :: "+Upper(FWSX2Util():GetX2Name("SRF",.F.))+" :: ["+DToC(Date())+"]["+Time()+"]","Wait!",.T.),oMSNewProcess:Activate())
                        MENUITEM "SR3 :: "+Upper(FWSX2Util():GetX2Name("SR3",.F.)) ACTION (oMSNewProcess:=MsNewProcess():New(bSR3MsNewProcess,"Importing SR3 :: "+Upper(FWSX2Util():GetX2Name("SR3",.F.))+" :: ["+DToC(Date())+"]["+Time()+"]","Wait!",.T.),oMSNewProcess:Activate())
                        MENUITEM "SR8 :: "+Upper(FWSX2Util():GetX2Name("SR8",.F.)) ACTION (oMSNewProcess:=MsNewProcess():New(bSR8MsNewProcess,"Importing SR8 :: "+Upper(FWSX2Util():GetX2Name("SR8",.F.))+" :: ["+DToC(Date())+"]["+Time()+"]","Wait!",.T.),oMSNewProcess:Activate())
                        MENUITEM "SP8 :: "+Upper(FWSX2Util():GetX2Name("SP8",.F.)) ACTION (oMSNewProcess:=MsNewProcess():New(bSP8MsNewProcess,"Importing SP8 :: "+Upper(FWSX2Util():GetX2Name("SP8",.F.))+" :: ["+DToC(Date())+"]["+Time()+"]","Wait!",.T.),oMSNewProcess:Activate())
                        MENUITEM "SRD :: "+Upper(FWSX2Util():GetX2Name("SRD",.F.)) ACTION (oMSNewProcess:=MsNewProcess():New(bSRDMsNewProcess,"Importing SRD :: "+Upper(FWSX2Util():GetX2Name("SRD",.F.))+" :: ["+DToC(Date())+"]["+Time()+"]","Wait!",.T.),oMSNewProcess:Activate())
                    ENDMENU
                ENDMENU
                MENUITEM "Tools"
                MENU
                    MENUITEM "Send data to the server" ACTION MsAguarde({||lSendDataToServer:=sendDataToServer(@cGetFileRoot,.T.)},"Sending Data To Server","Wait!")
                    MENUITEM "Settings" ACTION CFGFile()
                ENDMENU
                MENU
                    MENUITEM "Exit" ACTION oDlg:End()
                ENDMENU
            ENDMENU

            oMenu:align:=CONTROL_ALIGN_LEFT
            oDlg:SetMenu(oMenu)

        ACTIVATE DIALOG oDlg CENTERED

        oFont:=FreeObj(oFont)
        oMenu:=FreeObj(oMenu)
        oDlg:=FreeObj(oDlg)
        oMSNewProcess:=FreeObj(oMSNewProcess)

    end sequence

    __SetCentury(if(lSetCentury,"ON","OFF"))

    cacheData():delSection("datasul2totvsprotheus")

    if (findFunction("RstPosAlias"))
        RstPosAlias()
    endif

    if (valType(st_oHMbTransform)=="O")
        st_oHMbTransform:Clean()
        st_oHMbTransform:=FreeObj(st_oHMbTransform)
    endif

    DelClassIntF()

return(lActivate)

static function readIniFile(cINIFile as character) as logical

    local cRootPath as character

    local cSPPDrive as character
    local cSPPPath as character
    local cSPPFile as character
    local cSPPExt as character

    local lreadIniFile:=.F. as logical

    local oTFIni as object

    begin sequence

        lreadIniFile:=(!empty(cINIFile).and.(File(cINIFile)))
        if (!lreadIniFile)
            ApMsgAlert("Configuration file invalid or not found","Attention")
            break
        endif

        SplitPath(cINIFile,@cSPPDrive,@cSPPPath,@cSPPFile,@cSPPExt)

        cRootPath:=cSPPDrive
        cRootPath+=cSPPPath

        cacheData():Set("datasul2totvsprotheus","cINIFile",cINIFile)
        cacheData():Set("datasul2totvsprotheus","cRootPath",cRootPath)

        oTFIni:=TFIni():New(cINIFile,"#")

        cacheData():Set("datasul2totvsprotheus","readIniFile",oTFIni)

    end sequence

return(lreadIniFile)

static function readTableDefinition() as logical

    local aFieldDef as array
    local aFieldDefTmp as array
    local aFieldsDef:=array(0) as array
    local aTablesImport as array

    local cLine as character
    local cLineUTF8 as character

    local cTable as character
    local cRootPath as character
    local cTablesDefinition as character
    local cFileTablesDefinition as character

    local lTypeReplicate:=.F. as logical
    local lSkipTable:=.F. as logical
    local lreadTableDefinition:=.F. as logical

    local nField as numeric
    local nFields as numeric

    local nft_fRecNo as numeric
    local nft_fHandle as numeric

    local nTypeReplicate as numeric

    local oFT as object
    local oTFIni as object
    local oHMTablesImport as object

    begin sequence

        oTFIni:=cacheData():Get("datasul2totvsprotheus","readIniFile")

        aTablesImport:=aClone(oTFIni:GetAllProperties("TablesImport"))
        lreadTableDefinition:=(!empty(aTablesImport))
        if (!lreadTableDefinition)
            ApMsgAlert("Invalid import tables","Attention")
            break
        endif

        cTablesDefinition:=allTrim(oTFIni:GetPropertyValue("Tables","TablesDefinition",""))
        lreadTableDefinition:=(!empty(cTablesDefinition))
        if (!lreadTableDefinition)
            ApMsgAlert("Invalid TablesDefinition","Attention")
            break
        endif

        cRootPath:=cacheData():Get("datasul2totvsprotheus","cRootPath")
        cFileTablesDefinition:=(cRootPath+".."+cTablesDefinition)
        lreadTableDefinition:=file(cFileTablesDefinition)
        if (!lreadTableDefinition)
            ApMsgAlert("File ("+cFileTablesDefinition+") with the definitions of the tables not found","Attention")
            break
        endif

        oFT:=uft():New()

        nft_fHandle:=oFT:ft_fUse(cFileTablesDefinition)
        lreadTableDefinition:=(nft_fHandle>=0)
        if (!lreadTableDefinition)
            oFT:ft_fUse()
            oFT:=FreeObj(oFT)
            ApMsgAlert("Could not open the file: "+cFileTablesDefinition,"Attention")
            break
        endif

        oHMTablesImport:=aToHM(aTablesImport,2)

        oFT:ft_fGoTop()

        while (!oFT:ft_fEof())
            cLine:=oFT:ft_fReadLN()
            begin sequence
                if (empty(cLine))
                    break
                endif
                cTable:=""
                if ("Table:"==Left(cLine,6))
                    cTable:=strTran(cLine,"Table:","")
                    cTable:=allTrim(cTable)
                    lSkipTable:=(!oHMTablesImport:Get(cTable))
                    if (lSkipTable)
                        break
                    else
                        oFT:ft_fDelete()
                        oFT:ft_fSkip()
                    endif
                    aSize(aFieldsDef,0)
                    while (!oFT:ft_fEof())
                        cLine:=oFT:ft_fReadLN()
                        if (empty(cLine).or.("Field Name"==Left(cLine,10)).or.("-"==Left(cLine,1)))
                            oFT:ft_fDelete()
                            oFT:ft_fSkip()
                            loop
                        endif
                        if ("Table:"==Left(cLine,6))
                            cacheData():Set("datasul2totvsprotheus",cTable,aClone(aFieldsDef))
                            aSize(aFieldsDef,0)
                            nft_fRecNo:=oFT:ft_fRecNo()
                            oFT:ft_fGoTo(nft_fRecNo-1)
                            break
                        endif
                        cLine:=cTools():StrReplace(cLine,{"  "," "})
                        cLineUTF8:=DecodeUTF8(cLine)
                        if (empty(cLineUTF8))
                            cLine:=OemToAnsi(cLine)
                        endif
                        cLineUTF8:=""
                        aFieldDef:=strTokArr2(cLine," ")
                        cLine:=""
                        if (len(aFieldDef)!=DTS_DBS_ALEN)
                            aFieldDefTmp:=array(DTS_DBS_ALEN)
                            aFieldDefTmp[DTS_DBS_NAME]:=aFieldDef[DTS_DBS_NAME]
                            aFieldDefTmp[DTS_DBS_TYPE]:=aFieldDef[DTS_DBS_TYPE]
                            aFieldDefTmp[DTS_DBS_FLG]:=""//DTS_DBS_FLG
                            aFieldDefTmp[DTS_DBS_FORMAT]:=aFieldDef[DTS_DBS_FLG]
                            aFieldDef:=aClone(aFieldDefTmp)
                            FWFreeArray(@aFieldDefTmp)
                        endif
                        nTypeReplicate:=AT("[",aFieldDef[DTS_DBS_TYPE])
                        lTypeReplicate:=(nTypeReplicate>0)
                        if (lTypeReplicate)
                            nFields:=val(subStr(aFieldDef[DTS_DBS_TYPE],(nTypeReplicate+1),(AT("]",aFieldDef[DTS_DBS_TYPE])-1)))
                            aAdd(aFieldsDef,aClone(aFieldDef))
                            if (nFields>1)
                                aFieldDefTmp:=aClone(aFieldDef)
                                cacheData():Set("datasul2totvsprotheus",cTable+"_TypeReplicate",lTypeReplicate)
                                cacheData():Set("datasul2totvsprotheus",aFieldDef[DTS_DBS_NAME]+"_TypeReplicate",nFields)
                                for nField:=2 to nFields
                                    aFieldDef[DTS_DBS_NAME]:=(aFieldDefTmp[DTS_DBS_NAME]+cTools():NToS(nField))
                                    aAdd(aFieldsDef,aClone(aFieldDef))
                                next nField
                                FWFreeArray(@aFieldDefTmp)
                            endif
                        else
                            aAdd(aFieldsDef,aClone(aFieldDef))
                        endif
                        FWFreeArray(@aFieldDef)
                        oFT:ft_fDelete()
                        oFT:ft_fSkip()
                    end while
                    cacheData():Set("datasul2totvsprotheus",cTable,aClone(aFieldsDef))
                    aSize(aFieldsDef,0)
                elseif (lSkipTable)
                    break
                endif
            end sequence
            oFT:ft_fDelete()
            oFT:ft_fSkip()
        end while

        oFT:ft_fUse()
        oFT:=FreeObj(oFT)

    end sequence

    FWFreeArray(@aFieldsDef)

    if (valtype(oHMTablesImport)=="O")
        oHMTablesImport:Clean()
        oHMTablesImport:=FreeObj(oHMTablesImport)
    endif

    if (valtype(aTablesImport)=="A")
        FWFreeArray(@aTablesImport)
    endif

    DelClassIntF()

return(lreadTableDefinition)

static function getTargetFieldValue(lHasError as logical,oTFIni as object,aLine as array,cTargetField as character,aTargetField as array,aSourceFieldsDef as array,oHMSourceFieldsDef as object,cType as character) as variant

    local aValueField as array
    local aSourceField as array

    local bTransform as codeblock

    local cTransform as character
    local cSourceField as character

    local lSourceTransform as logical

    local nLineSize:=Len(aLine) as numeric
    local nSourceField as numeric
    local nSourceFields as numeric
    local nSourceFieldAT as numeric

    local xValue as variant

    begin sequence

        cSourceField:=aTargetField[2]
        lSourceTransform:=("@"==Left(cSourceField,1))
        if (lSourceTransform)
            cSourceField:=subStr(cSourceField,2)
        endif
        aSourceField:=strToKArr2(cSourceField,"+")
        nSourceFields:=len(aSourceField)
        aValueField:=Array(nSourceFields,2)
        for nSourceField:=1 to nSourceFields
            cSourceField:=aSourceField[nSourceField]
            if (!oHMSourceFieldsDef:Get(cSourceField,@cType))
                loop
            endif
            if (!oHMSourceFieldsDef:Get(cSourceField+"_AT",@nSourceFieldAT))
                loop
            endif
            lHasError:=(nSourceFieldAT>nLineSize)
            if (lHasError)
                break
            endif
            aValueField[nSourceField][1]:=NormalizeData(@lHasError,aLine[nSourceFieldAT],@cType,@cSourceField,@lSourceTransform,@st_oHMbTransform,@oTFIni)
            if (lHasError)
                break
            endif
            aValueField[nSourceField][2]:=cType
            if (cType=="char")
                xValue:=""
            endif
        next nSourceField

        for nSourceField:=1 to nSourceFields
            if (aValueField[nSourceField][2]=="char")
                xValue+=aValueField[nSourceField][1]
            else
                xValue:=aValueField[nSourceField][1]
            endif
        next nSourceField

        if (!st_oHMbTransform:Get(cTargetField,@bTransform))
            cTransform:=allTrim(oTFIni:GetPropertyValue(cTargetField,"Transform",""))
            if (cTransform=="FindInTable")
                cTransform:=allTrim(oTFIni:GetPropertyValue(cTargetField,cTransform,""))
                lHasError:=(!EvalBlock():EvalBlock({||bTransform:=&(cTransform)},nil,.F.,nil,nil))
            elseif (!empty(cTransform))
                lHasError:=(!EvalBlock():EvalBlock({||bTransform:=&(cTransform)},nil,.F.,nil,nil))
            endif
            st_oHMbTransform:Set(cTargetField,bTransform)
        endif

        if (valtype(bTransform)=="B")
            lHasError:=(!EvalBlock():EvalBlock({||xValue:=Eval(bTransform,xValue,@oTFIni,@cType)},nil,.F.,nil,nil))
        endif

    end sequence

    FWFreeArray(@aValueField)
    FwFreeArray(@aSourceField)

return(xValue)

static function NormalizeData(lHasError as logical,xValue as variant,cType as character,cSourceField as character,lSourceTransform as logical,st_oHMbTransform as object,oTFIni as object) as variant

    local bTransform as codeblock

    local cTransform as character

    if (lSourceTransform)
        if (!st_oHMbTransform:Get(cSourceField,@bTransform))
            cTransform:=allTrim(oTFIni:GetPropertyValue(cSourceField,"Transform",""))
            if (cTransform=="FindInTable")
                cTransform:=allTrim(oTFIni:GetPropertyValue(cSourceField,cTransform,""))
                lHasError:=(!EvalBlock():EvalBlock({||bTransform:=&(cTransform)},nil,.F.,nil,nil))
            elseif (!empty(cTransform))
                lHasError:=(!EvalBlock():EvalBlock({||bTransform:=&(cTransform)},nil,.F.,nil,nil))
            endif
            if (!lHasError)
                st_oHMbTransform:Set(cSourceField,bTransform)
            endif
        endif
        if (valtype(bTransform)=="B")
            lHasError:=(!EvalBlock():EvalBlock({||xValue:=Eval(bTransform,xValue,oTFIni,@cType)},nil,.F.,nil,nil))
            if (!lHasError)
                xValue:=NormalizeData(@lHasError,xValue,@cType,nil,.F.,nil,nil)
            endif
        endif
    else
        if (valType(xValue)=="C")
            if ("char"$cType)
                xValue:=strTran(xValue,'"',"")
            elseif ("inte"$cType)
                xValue:=Int(val(xValue))
            elseif ("deci"$cType)
                if (","$xValue)
                    xValue:=strTran(xValue,",",".")
                endif
                xValue:=val(xValue)
            elseif ("date"$cType)
                xValue:=CToD(xValue)
            elseif ("logi"$cType)
                xValue:=(Left(lower(xValue),1)$"sty1")
            endif
        endif
    endif

return(xValue)

static function FindInTable(oTFIni as object,cTable as character,xValue as variant,lTableInFile as logical)

    local cType as character

    local cINIFile as character
    local cRootPath as character

    local oTFIniTable as object

    local xTmp as variant

    cType:=valType(xValue)
    if (cType=="N")
        xValue:=ctools():NToS(xValue)
    elseif (cType!="C")
        xValue:=cValToChar(xValue)
    endif

    if (oTFIni:ExistSection(cTable))
        if (oTFIni:GetATProperty(cTable,"FindInTableFile")>0)
            dfv():Default(@lTableInFile,.F.)
            if (lTableInFile)
                oTFIniTable:=cacheData():Get("datasul2totvsprotheus",cTable,nil)
                if (oTFIniTable==nil)
                    cRootPath:=cacheData():Get("datasul2totvsprotheus","cRootPath")
                    cINIFile:=cRootPath
                    cINIFile+=oTFIni:GetPropertyValue(cTable,"FindInTableFile","")
                    oTFIniTable:=TFIni():New(cINIFile,"#")
                    cacheData():Set("datasul2totvsprotheus",cTable,oTFIniTable)
                endif
                xTmp:=oTFIniTable:GetPropertyValue(cTable,xValue,"")
                if ((empty(xTmp)).and.((oTFIniTable:GetATProperty(cTable,"__DFV__")>0)))
                    xTmp:=oTFIniTable:GetPropertyValue(cTable,"__DFV__","")
                endif
                xValue:=xTmp
            else
                oTFIniTable:=cacheData():Get("datasul2totvsprotheus",cTable,nil)
                if (oTFIniTable==nil)
                    cRootPath:=cacheData():Get("datasul2totvsprotheus","cRootPath")
                    cINIFile:=cRootPath
                    cINIFile+=oTFIni:GetPropertyValue(cTable,"FindInTableFile","")
                    oTFIniTable:=TFIni():New(cINIFile,"#")
                    cacheData():Set("datasul2totvsprotheus",cTable,oTFIniTable)
                endif
                xValue:=FindInTable(oTFIniTable,cTable,xValue)
            endif
        elseif ((oTFIni:GetATProperty(cTable,xValue)>0))
            xValue:=oTFIni:GetPropertyValue(cTable,xValue,"")
        elseif ((oTFIni:GetATProperty(cTable,"__DFV__")>0))
            xValue:=oTFIni:GetPropertyValue(cTable,"__DFV__","")
        endif
    endif

return(xValue)

static function getR8Seq() as character

    static c_stHMSR8SeqKey as character

    local cStack:="DATASUL2TOTVS.IMPORTSR8" as character

    local cR8MAT as character
    local cR8SEQ:="" as character
    local cR8TIPO as character
    local cR8FILIAL as character
    local cR8DATAINI as character
    local cR8TIPOAFA as character

    local cHMSR8SeqKey:="" as character

    local cTmpAlias as character

    local dR8DATAINI as date

    begin sequence

        if (type("cPVTSR8Alias")!="C")
            _SetNamedPRVT("cPVTSR8Alias",cacheData():Get("datasul2totvsprotheus","importSR8Alias","__NOALIAS__"),cStack)
        endif

        cTmpAlias:=cPVTSR8Alias
        if (select(cTmpAlias)==0)
            break
        endif

        cR8FILIAL:=(cTmpAlias)->R8_FILIAL
        cR8MAT:=(cTmpAlias)->R8_MAT
        dR8DATAINI:=(cTmpAlias)->R8_DATAINI
        cR8TIPO:=(cTmpAlias)->R8_TIPO
        cR8TIPOAFA:=(cTmpAlias)->R8_TIPOAFA

        cR8DATAINI:=DToS(dR8DATAINI)

        cHMSR8SeqKey:=&("cEmpAnt")
        cHMSR8SeqKey+=cR8FILIAL
        cHMSR8SeqKey+=cR8MAT

        if (type("oPVTHMSR8Seq")!="O")
            _SetNamedPRVT("oPVTHMSR8Seq",THashMap():New(),cStack)
        else
            if (c_stHMSR8SeqKey!=cHMSR8SeqKey)
                c_stHMSR8SeqKey:=cHMSR8SeqKey
                oPVTHMSR8Seq:Clean()
            endif
        endif

        cHMSR8SeqKey+=cR8DATAINI
        cHMSR8SeqKey+=cR8TIPO
        cHMSR8SeqKey+=cR8TIPOAFA

        if (!oPVTHMSR8Seq:Get(cHMSR8SeqKey,@cR8SEQ)).or.(Empty(cR8SEQ))
            if (type("nPVTSR8Seq")!="N")
                _SetNamedPRVT("nPVTSR8Seq",GetSx3Cache("R8_SEQ","X3_TAMANHO"),cStack)
                cR8SEQ:=Replicate("0",nPVTSR8Seq)
                _SetNamedPRVT("cPVTR8SEQ",cR8SEQ,cStack)
            endif
            cR8SEQ:=cPVTR8SEQ
        endif

        cR8SEQ:=__Soma1(cR8SEQ)

        oPVTHMSR8Seq:Set(cHMSR8SeqKey,cR8SEQ)

    end sequence

return(cR8SEQ)

static function ProcRedefine(oProcess as object,oFont as object,nLeft as numeric,nWidth as numeric,nCTLFLeft as numeric,lODlgF as logical,lODlgW as logical) as logical
    local lProcRedefine as logical
    lProcRedefine:=MsProcess():ProcRedefine(@oProcess,@oFont,@nLeft,@nWidth,@nCTLFLeft,@lODlgF,@lODlgW)
    return(lProcRedefine)

static function SetFilAnt(cFil)
    local cSvFilAnt:=&("cFilAnt") as character
    if (valType("cFilAnt")=="C")
        cSvFilAnt:=&("cFilAnt")
        if (cFil!=cSvFilAnt)
            &("cFilAnt"):=cFil
            FWSM0Util():setSM0PositionBycFilAnt()
        endif
    endif
return(cSvFilAnt)

static procedure CFGFile()

    local cINIFile as character
    local cMemoEdit as character
    local cGetFileRoot as character
    local cGetFileMask as character
    local cGetFileTitle as character

    local lHasINIFile as logical

    local nGetFileOptions as numeric

    begin sequence

        cGetFileRoot:=cacheData():Get("datasul2totvsprotheus","cRootPath")
        cGetFileMask:="Arquivo de Configuração | *.ini"
        cGetFileTitle:="Arquivo de Configuração *.ini"
        nGetFileOptions:=(GETF_LOCALHARD+GETF_NETWORKDRIVE)
        cINIFile:=cGetFile(cGetFileMask,cGetFileTitle,1,cGetFileRoot,.F.,nGetFileOptions,/*[lArvore]*/,/*[lKeepCase]*/)

        lHasINIFile:=File(cINIFile)
        if (!lHasINIFile)
            break
        endif

        cMemoEdit:=ReadMemoCFGFile(cINIFile)

        if (EdtCFGFile(@cINIFile,@cMemoEdit))
            if (!("datasul2protheus.ini"$lower(cINIFile)))
                cINIFile:=cacheData():Get("datasul2totvsprotheus","cINIFile")
            endif
            cacheData():delSection("datasul2totvsprotheus")
            if (valType(st_oHMbTransform)=="O")
                st_oHMbTransform:Clean()
            endif
            MsAguarde({||readIniFile(@cINIFile)},"Loading Settings","Wait")
            MsAguarde({||readTableDefinition()},"Loading structure of tables","Wait")
        endif

    end sequence

    return

static function EdtCFGFile(cINIFile as character,cMemoEdit as character) as logical

    local aButtons as array
    local aAdvSize as array
    local aObjSize as array
    local aObjCoords as array
    local aInfoAdvSize as array

    local bSet15 as codeblock
    local bSet24 as codeblock
    local bNotepadCFGFile as codeblock
    local bRestoreCFGFile as codeblock

    local cTitle as character
    local cSVMemoEdit as character

    local lOk as logical

    local nDlgWidth as numeric

    local oDlg as object
    local oFont as object
    local oPanel as object
    local oMemoEdit as object

    dfv():Default(@cMemoEdit,"")

    cSVMemoEdit:=cMemoEdit

    begin sequence

        aAdvSize:=MsAdvSize()
        aInfoAdvSize:={aAdvSize[1],aAdvSize[2],aAdvSize[3],aAdvSize[4],0,0}
        aObjCoords:=array(0)
        aAdd(aObjCoords,{0,0,.T.,.T.})
        aObjSize:=MsObjSize(aInfoAdvSize,aObjCoords)

        cTitle:="ARQUIVO CONFIGURACAO "
        cTitle+=" :: "
        cTitle+=cINIFile

        DEFINE FONT oFont NAME "Courier New" SIZE 0,-11 BOLD
        DEFINE MSDIALOG oDlg TITLE cTitle FROM aAdvSize[7],0 TO aAdvSize[6],aAdvSize[5] OF GetWndDefault() STYLE DS_MODALFRAME STATUS PIXEL

            nDlgWidth:=__DlgWidth(oDlg)

            oPanel:=tPanel():New(0,0,"",oDlg,nil,nil,nil,nil,nil,nDlgWidth,nDlgWidth)

            @(aObjSize[1][1]+5),(aObjSize[1][2]+5) GET oMemoEdit VAR cMemoEdit MEMO SIZE ((aObjSize[1][4]/100)*99),((aObjSize[1][3]/100)*88) FONT oFont CENTERED DESIGN OF oPanel PIXEL WHEN (.T.)

            oMemoEdit:lReadOnly:=.F.
            oMemoEdit:EnableVScroll(.T.)
            oMemoEdit:EnableHScroll(.T.)

            oDlg:lEscClose:=.F.

            bSet15:={||lOk:=ChgCFGFile(@cINIFile,@cMemoEdit,@cSVMemoEdit),if(lOk,oDlg:End(),lOK)}
            bSet24:={||lOK:=.F.,oDlg:End()}

            aButtons:=array(0)
            bNotepadCFGFile:={||MsProcess():MsAguarde({||NotepadCFGFile(cINIFile)},"Abrindo no NotepadCFGFile","Aguarde...")}
            aAdd(aButtons,{"NotepadCFGFile",bNotepadCFGFile,OemToAnsi("Abir no NotepadCFGFile"),OemtoAnsi("Abir no NotepadCFGFile")})
            bRestoreCFGFile:={||MsProcess():MsAguarde({||RestoreCFGFile(cINIFile,@cMemoEdit,@oMemoEdit,@cSVMemoEdit)},"Restaurando Versao Anterior","Aguarde...")}
            aAdd(aButtons,{"RestoreCFGFile",bRestoreCFGFile,OemToAnsi("Restaurar Versao Anterior"),OemtoAnsi("Restaurar Versao Anterior")})

        ACTIVATE MSDIALOG oDlg CENTERED ON INIT EnchoiceBar(oDlg,bSet15,bSet24,nil,@aButtons)

    end sequence

    return(lOk)

static function ChgCFGFile(cINIFile as character,cMemoEdit as character,cSVMemoEdit as character) as logical

    local cMsg as character
    local cTitle as character

    local cSPDriver as character
    local cSPPath as character
    local cSPFileName as character
    local cSPFileExt as character

    local cFWWSBKP as character

    local lFilial as logical
    local lEmpresa as logical
    local lChgCFGFile as logical

    local nAttemps as numeric

    lFilial:=(type("cFilAnt")=="C")
    lEmpresa:=(type("cEmpAnt")=="C")

    begin sequence

        lChgCFGFile:=(cMemoEdit==cSVMemoEdit)
        if (lChgCFGFile)
            break
        endif

        cSPDriver:=""
        cSPPath:=""
        cSPFileName:=""
        cSPFileExt:=""

        SplitPath(cINIFile,@cSPDriver,@cSPPath,@cSPFileName,@cSPFileExt)

        cMsg:="Deseja Gravar as Alteracoes?"

        cTitle:="ARQUIVO CONFIGURACAO  :: "
        cTitle+=cSPFileName

        lChgCFGFile:=ApMsgYesNo(cMsg,cTitle)
        if (!lChgCFGFile)
            break
        endif

        cFWWSBKP:=cSPDriver
        cFWWSBKP+=cSPPath
        cFWWSBKP+=cSPFileName
        cFWWSBKP+=cSPFileExt
        cFWWSBKP+="-"
        cFWWSBKP+=DToS(Date())
        cFWWSBKP+="-"
        cFWWSBKP+=strTran(Time(),":","-")
        cFWWSBKP+="-"
        cFWWSBKP+=strZero(ranDomize(1,999),3)

        nAttemps:=0

        cMsg:="O arquivo "
        cMsg+=cINIFile
        cMsg+=" encontra-se em uso deseja tentar novamente?"

        while (!LockByName(cSPFileName,lEmpresa,lFilial,.T.))
            if (nAttemps++>10)
                nAttemps:=0
                if (!MsgYesNo(cMsg,cTitle))
                    lChgCFGFile:=.F.
                    cMsg:="O arquivo "
                    cMsg+=cINIFile
                    cMsg+=" encontra-se em uso e nao podera ser salvo."
                    ApMsgAlert(cMsg,cTitle)
                    break
                endif
            endif
            sleep(300)
        end while

        nAttemps:=0

        while (file(cINIFile))
            if (fRename(cINIFile,cFWWSBKP))
                exit
            endif
            if (nAttemps++>10)
                nAttemps:=0
                if (!MsgYesNo(cMsg,cTitle))
                    UnlockByName(cSPFileName,lEmpresa,lFilial,.T.)
                    lChgCFGFile:=.F.
                    cMsg:="Problema na gravacao do arquivo : "
                    cMsg+=cINIFile
                    ApMsgAlert(cMsg,cTitle)
                    break
                endif
            endif
            sleep(300)
        end while

        nAttemps:=0

        cMsg:="Problema na gravacao do arquivo : "
        cMsg+=cINIFile
        cMsg+=" Deseja tentar novamente?"

        while (!(lChgCFGFile:=MemoWrite(cINIFile,cMemoEdit)))
            if (nAttemps++>10)
                nAttemps:=0
                if (!MsgYesNo(cMsg,cTitle))
                    exit
                endif
            endif
            sleep(300)
        end while

        UnlockByName(cSPFileName,lEmpresa,lFilial,.T.)

        if (!lChgCFGFile)
            cMsg:="Problema na gravacao do arquivo : "
            cMsg+=cINIFile
            ApMsgAlert(cMsg,cTitle)
            break
        endif

        cMsg:="Arquivo "
        cMsg+=cINIFile
        cMsg+=" salvo com sucesso."
        ApMsgInfo(cMsg,cTitle)

    end sequence

    return(lChgCFGFile)

static procedure NotepadCFGFile(cINIFile as character)

    local cSPDriver as character
    local cSPPath as character
    local cSPFileName as character
    local cSPFileExt as character

    local cTempPath as character
    local cTempFile as character

    cSPDriver:=""
    cSPPath:=""
    cSPFileName:=""
    cSPFileExt:=""

    SplitPath(cINIFile,@cSPDriver,@cSPPath,@cSPFileName,@cSPFileExt)

    cTempPath:=getTempPath()
    if (!(right(cTempPath,1)=="\"))
        cTempPath+="\"
    endif

    cTempFile:=cTempPath
    cTempFile+=cSPFileName
    cTempFile+=cSPFileExt

    if (__CopyFile(cINIFile,cTempFile))
        ShellExecute("open",cTempFile,"","",SW_SHOW)
    endif

    return

static function RestoreCFGFile(cINIFile as character,cMemoEdit as character,oMemoEdit as object,cSVMemoEdit as character) as logical

    local cMask as character
    local cTitle as character
    local cFileRead as character
    local cDirectory as character

    local cSPDriver as character
    local cSPPath as character
    local cSPFileName as character
    local cSPFileExt as character

    local lRestoreCFGFile as logical

    local nOptions as numeric

    begin sequence

        cSPDriver:=""
        cSPPath:=""
        cSPFileName:=""
        cSPFileExt:=""

        SplitPath(cINIFile,@cSPDriver,@cSPPath,@cSPFileName,@cSPFileExt)

        cMask:="Arquivos de Configuracao|"+cSPFileName+"*"
        cTitle:="Restaurar arquivo de Configuracao"
        cDirectory:=cSPDriver
        cDirectory+=cSPPath
        cDirectory:=StrTran(cSPPath,"/","\")
        nOptions:=GETF_NOCHANGEDIR

        cFileRead:=cGetFile(cMask,cTitle,1,cDirectory,.F.,nOptions,/*[lArvore]*/,/*[lKeepCase]*/)

        if (empty(cFileRead))
            break
        endif

        cFileRead:=(cDirectory+cFileRead)
        cFileRead:=StrTran(cFileRead,"\\","\")

        if (!file(cFileRead))
            break
        endif

        cSvMemoEdit:=cMemoEdit
        cMemoEdit:=ReadMemoCFGFile(cFileRead)

        lRestoreCFGFile:=ChgCFGFile(@cINIFile,@cMemoEdit,@cSVMemoEdit)

        if (!lRestoreCFGFile)
            cMemoEdit:=cSvMemoEdit
            break
        endif

        cSvMemoEdit:=cMemoEdit

        oMemoEdit:Refresh()

    end sequence

    return(lRestoreCFGFile)

static function ReadMemoCFGFile(cFile) as character
    local cMemoRead as character
    cMemoRead:=cTools():ReadMemo(cFile)
    return(cMemoRead)

static function sendDataToServer(cGetFileRoot as character,lReloadConfig as logical) as logical

    local aZipFiles:=array(0) as array

    local aFiles as array
    local aDirectories as array

    local cFile as character
    local cZipFile as character
    local cINIFile as character
    local cDirectory as character
    local cGetFileMask:="" as character
    local cGetFileTitle as character
    local cZipFileServer as character
    local cLowerDirectory as character
    local cDirectoryClient as character
    local cDirectoryServer as character

    local cSPPDrive as character
    local cSPPPath as character
    local cSPPFile as character
    local cSPPExt as character

    local lSendDataToServer as logical

    local nFile as numeric
    local nFiles as numeric
    local nDirectory as numeric
    local nDirectories as numeric
    local nGetFileOptions as numeric

    begin sequence

        lSendDataToServer:=(getComputerName()$(GetNewPar("TOTVSD2PCN","DNA-CSDJ-01")))
        if (!lSendDataToServer)
            break
        endif

        cGetFileTitle:="Diretorio de Configuração DataSul2Protheus.ini"
        nGetFileOptions:=(GETF_LOCALHARD+GETF_NETWORKDRIVE+GETF_RETDIRECTORY)
        cDirectoryClient:=cGetFile(cGetFileMask,cGetFileTitle,1,".\",.F.,nGetFileOptions,/*[lArvore]*/,/*[lKeepCase]*/)

        lSendDataToServer:=((lIsDir(cDirectoryClient)).and.("datasul2protheus"$StrTran(lower(cDirectoryClient),"totvs","")))
        if (!lSendDataToServer)
            if (ApMsgYesNo("Invalid directory ["+cDirectoryClient+"]. Want to try again?"))
                lSendDataToServer:=sendDataToServer(@cGetFileRoot)
            elseif (file("\datasul2Protheus\ini\DataSul2Protheus.ini"))
                cGetFileRoot:="SERVIDOR\datasul2protheus\ini\"
            endif
            break
        elseif (file("\datasul2Protheus\ini\DataSul2Protheus.ini"))
            cGetFileRoot:="SERVIDOR\datasul2protheus\ini\"
        endif

        cDirectoryServer:="\datasul2Protheus\"
        DirTools():MakeFileDir(cDirectoryServer)
        lSendDataToServer:=lIsDir(cDirectoryServer)
        if (!lSendDataToServer)
            ApMsgAlert("Invalid directory ["+cDirectoryServer+"]")
            break
        endif

        aDirectories:=Directory(cDirectoryClient+"*.*","D")
        nDirectories:=Len(aDirectories)

        lSendDataToServer:=(nDirectories!=0)
        if (!lSendDataToServer)
            ApMsgAlert("No valid directory information found")
            break
        endif

        for nDirectory:=1 to nDirectories
            if (aDirectories[nDirectory][5]!="D")
                loop
            endif
            cDirectory:=cDirectoryClient+aDirectories[nDirectory][1]
            if (right(cDirectory,1)==".")
                loop
            endif
            cLowerDirectory:=lower(cDirectory)
            if (("7z"$cLowerDirectory).or.("src"$cLowerDirectory).or.(".gitignore"$cLowerDirectory).or.(".vscode"$cLowerDirectory))
                loop
            endif
            if (Right(cDirectory,1)!="\")
                cDirectory+="\"
            endif
            aFiles:=Directory(cDirectory+"*.*")
            nFiles:=Len(aFiles)
            for nFile:=1 To nFiles
                MsProcTxt(aFiles[nFile][1])
                cFile:=cDirectory+aFiles[nFile][1]
                aAdd(aZipFiles,cFile)
                cFile:=cDirectoryServer+aFiles[nFile][1]
                if (file(cFile))
                    fErase(cFile)
                endif
            next nFile
        next nDirectory

        cZipFile:=(getTempPath()+criatrab(nil,.F.)+".zip")
        MsProcTxt("ZipFile. Wait...")
        lSendDataToServer:=(FZip(cZipFile,aZipFiles,cDirectoryClient,/*,[cSenha]*/)==0)
        if (!lSendDataToServer)
            ApMsgAlert("Problem zipping ["+cZipFile+"] file")
            break
        endif

        SplitPath(cZipFile,@cSPPDrive,@cSPPPath,@cSPPFile,@cSPPExt)

        cZipFileServer:=cDirectoryServer
        cZipFileServer+=cSPPFile
        cZipFileServer+=cSPPExt

        MsProcTxt("Wait...")
        lSendDataToServer:=__CopyFile(cZipFile,cZipFileServer)
        if (!lSendDataToServer)
            ApMsgAlert("Problem copying ["+cZipFile+"] file to server")
            break
        endif

        MsProcTxt("UnZipFile. Wait...")
        lSendDataToServer:=(FUnzip(cZipFileServer,cDirectoryServer,/*[cSenha]*/)==0)
        if (!lSendDataToServer)
            ApMsgAlert("Problem unzipping the test ["+cZipFile+"] on the server")
            break
        endif

        cGetFileRoot:="SERVIDOR"+cDirectoryServer

        ApMsgInfo("Files uploaded to the server successfully. Directory ["+cGetFileRoot+"]")

        cGetFileRoot+="ini\"

    end sequence

    FWFreeArray(@aFiles)
    FWFreeArray(@aZipFiles)
    FWFreeArray(@aDirectories)

    if (!empty(cZipFile)).and.(file(cZipFile))
        fErase(cZipFile)
    endif

    if ((!empty(cZipFileServer)).and.(file(cZipFileServer)))
        fErase(cZipFileServer)
    endif

    if (lSendDataToServer)
        dfv():Default(@lReloadConfig,.F.)
        if (lReloadConfig)
            cacheData():delSection("datasul2totvsprotheus")
            if (valType(st_oHMbTransform)=="O")
                st_oHMbTransform:Clean()
            endif
            cINIFile:="\datasul2Protheus\ini\DataSul2Protheus.ini"
            MsAguarde({||readIniFile(@cINIFile)},"Loading Settings","Wait")
            MsAguarde({||readTableDefinition()},"Loading structure of tables","Wait")
        endif
    endif

return(lSendDataToServer)

static function ImportData(oMsNewProcess as object,lEnd as logical,oTLogReport as object,cAlias as character,cIndexKey as character,cTableImport as character,aAddIndex as array,lPreLoadTable as logical,bCustomUpdate as codeblock) as logical

    local aLine as array
    local aField as array

    local aFieldsDef:=array(0) as array

    local aTargetStruct:=(cAlias)->(dbStruct()) as array
    local aTargetFieldsDef as array
    local aReplicateValues as array
    local aTargetReplicateFields as array
    local aTargetReplicateFieldsPos as array
    local aTargetFieldsDefReplicate as array

    local aUpdateTableFields as array
    local aUpdateTableSource as array

    local bDeleteIfEmpty as codeblock
    local bAfterUpdateRecNo as codeblock
    local bUpdateMemoVirtual as codeblock
    
    local bEnableFilterBlock as codeblock
    local bEnableFilterBlockMem as codeblock

    local cError as character
    local cField as character
    local cLineUTF8 as character

    local cTmpAlias:=getNextAlias() as character

    local cType as character

    local cLine as character
    local cRootPath as character
    local cDataPath as character
    local cSourceFile as character
    local cSourceTable as character

    local cDeleteIfEmpty:="" as character

    local cSVFilAnt:=&("cFilAnt") as character

    local cRecCount as character

    local cTLogReport as character

    local cIndexID as character
    local cFieldFilial as character
    local cAliasxFilial as character

    local cIncProc as character
    local cTimeRemaining as character

    local cSeekTable as character
    local cSeekIndexKey as character
    local cSeekTableIndex as character
    local cAllSeekIndexKey as character
    local cUpdateTableFields as character
    local cUpdateTableSource as character

    local cFWTemporaryTableRealName as character

    local cFieldsFilterBlock as character

    local lFound as logical
    local lAddNew as logical
    local lAppend as logical
    local ldbCommit as logical
    local lHasError:=.F. as logical
    local lChkFilial as logical
    local lChkEncode as logical
    local lSeekTable as logical
    local lImportData as logical
    local lMemoVirutal as logical
    local lRecCountStep as logical
    local lCustomUpdate:=.F. as logical
    local lTypeReplicate:=.F. as logical
    local lAppendIfNotExist as logical
    local lUpdateEmptyValue:=.F. as logical
    local lUpdateTableFields:=.F. as logical
    local lEnableFilterBlock as logical

    local nField as numeric
    local nFields as numeric

    local nReplicate as numeric
    local nReplicates:=0 as numeric

    local nRecProc:=0 as numeric
    local nRecCount as numeric
    local nRecCountStep as numeric
    local nCommitBuffer as numeric
    local nTargetFieldPos as numeric

    local nReplicateValuesAT as numeric

    local nIndexKey:=retOrder(cAlias,cIndexKey,.T.) as numeric
    local nSeekTableIndex as numeric
    local nUpdateTableSource as numeric

    local nFieldFilial as numeric

    local nIncProcStep as numeric

    local nDeleteIfEmpty:=0 as numeric

    local nft_fRecNo as numeric
    local nft_fHandle as numeric

    local nDBS_ALEN:=(DBS_ALEN+1) as numeric

    local nMLCountSize:=150 as numeric

    local nAliasdbCount:=0 as numeric

    local nTmpAliasOrder as numeric
    local nTmpAliasRecNo as numeric
    local nTmpAliasNextRecNo:=(-1) as numeric

    local oFT as object
    local oTFIni as object
    local oProgress as object
    local oHMMemoVirutal as object
    local oHMTargetStruct as object
    local oTimeRemaining as object
    local oFWTemporaryTable as object
    local oHMSourceFieldsDef as object
    local oHMTargetReplicateFields as object

    begin sequence

        lImportData:=(nIndexKey!=0)
        if (!lImportData)
            ApMsgAlert("Unlocalized "+cAlias+" table search index ("+cIndexKey+")")
            break
        endif

        oTFIni:=cacheData():Get("datasul2totvsprotheus","readIniFile")

        cFieldFilial:=PrefixoCpo(cAlias)
        cFieldFilial+="_FILIAL"

        lChkFilial:=(allTrim(oTFIni:GetPropertyValue(cAlias,"ChkFilial","1"))=="1")
        cAliasxFilial:=xFilial(cAlias)

        if (cAlias==cTableImport)
            if (lChkFilial)
                nFieldFilial:=(cAlias)->(FieldPos(cFieldFilial))
                if (left(cIndexKey,Len(cFieldFilial))==cFieldFilial)
                    (cAlias)->(dbSetOrder(nIndexKey))
                    (cAlias)->(dbSeek(cAliasxFilial,.T.))
                else
                    (cAlias)->(dbGoTop())
                endif
            endif
            MsAguarde({||dbEval():Eval(cAlias,{||++nAliasdbCount},{||!deleted().and.if(lChkFilial,FieldGet(nFieldFilial)==cAliasxFilial,.T.)},{||nAliasdbCount==0},nil,nil,.F.,{||dbGoTop()})})
            if (nAliasdbCount>0)
                lImportData:=ApMsgNoYes("There is already data in this table. Do you want to update them?","Info")
                if (!lImportData)
                    break
                endif
            endif
        endif

        cSourceTable:=allTrim(oTFIni:GetPropertyValue("TablesImport",cTableImport,""))
        lImportData:=(!empty(cSourceTable))
        if (!lImportData)
            ApMsgAlert("There is no setting for the SRA table","Attention")
            break
        endif

        cDataPath:=allTrim(oTFIni:GetPropertyValue("Tables","TablesData",""))
        lImportData:=(!empty(cDataPath))
        if (!lImportData)
            ApMsgAlert("Invalida Data Path ("+cDataPath+")","Attention")
            break
        endif

        cRootPath:=cacheData():Get("datasul2totvsprotheus","cRootPath")
        cSourceFile:=(cRootPath+".."+cDataPath+cSourceTable+".csv")
        lImportData:=file(cSourceFile)
        if (!lImportData)
            ApMsgAlert("File ("+cSourceFile+") not found","Attention")
            break
        endif

        aFieldsDef:=aClone(cacheData():Get("datasul2totvsprotheus",cSourceTable,aFieldsDef))
        lImportData:=(!empty(aFieldsDef))
        if (!lImportData)
            ApMsgAlert("Field definitions for the "+cSourceTable+" table not found","Attention")
            break
        endif

        lTypeReplicate:=cacheData():Get("datasul2totvsprotheus",cSourceTable+"_TypeReplicate",lTypeReplicate)
        if (lTypeReplicate)
            lTypeReplicate:=(Lower(allTrim(oTFIni:GetPropertyValue(cSourceTable,"TypeReplicate","")))=="row")
        endif

        oHMSourceFieldsDef:=THashMap():New()
        nFields:=Len(aFieldsDef)
        for nField:=1 to nFields
            oHMSourceFieldsDef:Set(aFieldsDef[nField][DTS_DBS_NAME],aFieldsDef[nField][DTS_DBS_TYPE])
            oHMSourceFieldsDef:Set(aFieldsDef[nField][DTS_DBS_NAME]+"_AT",nField)
        next nField

        aTargetFieldsDef:=aClone(oTFIni:GetAllProperties(cSourceTable))
        lImportData:=(!empty(aFieldsDef))
        if (!lImportData)
            ApMsgAlert("Field definitions for the "+cTableImport+" table not found","Attention")
            break
        endif

        lMemoVirutal:=(allTrim(oTFIni:GetPropertyValue(cAlias,"MemoVirtual","0"))=="1")
        if (lMemoVirutal)
            oHMMemoVirutal:=THashMap():New()
        endif

        if (lMemoVirutal)
            nFields:=Len(aTargetStruct)
            for nField:=1 to nFields
                cField:=aTargetStruct[nField][DBS_NAME]
                if (allTrim(oTFIni:GetPropertyValue(cField,"MemoVirtual","0"))=="1")
                    bUpdateMemoVirtual:=&(allTrim(oTFIni:GetPropertyValue(cField,"bUpdateMemoVirtual","{||}")))
                    oHMMemoVirutal:Set(cField,bUpdateMemoVirtual)
                endif
            next nField
        endif
        oHMTargetStruct:=aUtils():dbStructHM(@aTargetStruct)

        nFields:=Len(aTargetFieldsDef)

        if (lTypeReplicate)
            aTargetReplicateFields:=array(0)
            oHMTargetReplicateFields:=THashMap():New()
        endif

        for nField:=1 to nFields
            cField:=aTargetFieldsDef[nField][1]
            if (oHMTargetStruct:Get(cField,@aField))
                nTargetFieldPos:=aField[1][nDBS_ALEN]
            else
                nTargetFieldPos:=0
            endif
            aAdd(aTargetFieldsDef[nField],nTargetFieldPos)
            if (lTypeReplicate).and.(nTargetFieldPos>0)
                aAdd(aTargetReplicateFields,cField)
                oHMTargetReplicateFields:Set(cField,Len(aTargetReplicateFields))
            endif
        next nField

        oHMTargetStruct:Clean()
        oHMTargetStruct:=FreeObj(oHMTargetStruct)

        oFT:=uft():New()

        MsAguarde({||nft_fHandle:=oFT:ft_fUse(cSourceFile)},"Loading Data File","Wait")
        lImportData:=(nft_fHandle>=0)
        if (!lImportData)
            ApMsgAlert("Could not open the file: "+cSourceFile,"Attention")
            break
        endif

        lImportData:=(Len(aTargetStruct)>=0)
        if (!lImportData)
            ApMsgAlert("Could not get structure for table:  "+cTableImport,"Attention")
            break
        endif

        oFWTemporaryTable:=FWTemporaryTable():New(@cTmpAlias,@aTargetStruct)
        oFWTemporaryTable:AddIndex("01",aAddIndex)
        oFWTemporaryTable:Create()

        cacheData():Set("datasul2totvsprotheus","import"+cTableImport+"Alias",cTmpAlias)
        cacheData():Set("datasul2totvsprotheus","import"+cTableImport+"FWTemporaryTable",oFWTemporaryTable)

        cFWTemporaryTableRealName:=oFWTemporaryTable:getRealName()
        cacheData():Set("datasul2totvsprotheus","import"+cTableImport+"Table",cFWTemporaryTableRealName)

        FWFreeArray(@aField)
        FWFreeArray(@aTargetStruct)

        lChkEncode:=(allTrim(oTFIni:GetPropertyValue(cAlias,"ChkEncode","0"))=="1")

        lEnableFilterBlock:=(allTrim(oTFIni:GetPropertyValue(cAlias,"EnableFilterBlock","0"))=="1")
        if (lEnableFilterBlock)
            bEnableFilterBlock:=cacheData():Get("datasul2totvsprotheus",cAlias+"EnableFilterBlock",{||.T.})
            cFieldsFilterBlock:=allTrim(oTFIni:GetPropertyValue(cAlias,"FieldsFilterBlock",""))
            bEnableFilterBlockMem:=cacheData():get("datasul2totvsprotheus",cAlias+"EnableFilterBlockMem",{||.T.})
        endif

        bDeleteIfEmpty:=&(allTrim(oTFIni:GetPropertyValue(cAlias,"bDeleteIfEmpty","{|cAlias|.F.}")))

        lSeekTable:=(!empty(cSeekTable:=allTrim(oTFIni:GetPropertyValue(cAlias,"seekTable",""))))
        if (lSeekTable)
            lSeekTable:=chkFile(cSeekTable)
            if (lSeekTable)
                cSeekIndexKey:=allTrim(oTFIni:GetPropertyValue(cAlias,"seekIndexKey",""))
                cSeekTableIndex:=allTrim(oTFIni:GetPropertyValue(cAlias,"seekTableIndex",""))
                nSeekTableIndex:=retOrder(cSeekTable,cSeekTableIndex,.T.)
                lSeekTable:=(nSeekTableIndex>0)
                if (lSeekTable)
                    (cSeekTable)->(dbSetOrder(nSeekTableIndex))
                    cUpdateTableFields:=allTrim(oTFIni:GetPropertyValue(cAlias,"updateTableFields",""))
                    aUpdateTableFields:=strToKArr2(cUpdateTableFields,",")
                    cUpdateTableSource:=allTrim(oTFIni:GetPropertyValue(cAlias,"updateTableSource",""))
                    aUpdateTableSource:=strToKArr2(cUpdateTableSource,",")
                    nUpdateTableSource:=len(aUpdateTableFields)
                    lUpdateTableFields:=((nUpdateTableSource>0).and.(nUpdateTableSource==len(aUpdateTableSource)))
                endif
            endif
        endif

        nIncProcStep:=Int(val(allTrim(oTFIni:GetPropertyValue(cAlias,"IncProcStep","0"))))
        if (empty(nIncProcStep))
            nIncProcStep:=Int(val(allTrim(oTFIni:GetPropertyValue("General","IncProcStep","1"))))
        endif
        nIncProcStep:=Min(Max(nIncProcStep,1),1000)

        begin sequence

            nRecCount:=oFT:ft_fLastRec()
            cRecCount:=cTools():NToS(nRecCount)
            nRecCountStep:=if(nRecCount>=(nIncProcStep*2),Int(nRecCount/nIncProcStep),nRecCount)
            lRecCountStep:=(nRecCountStep!=nRecCount)
            if (lRecCountStep)
                nRecCountStep+=(nRecCount-(nRecCountStep*nIncProcStep))
            endif
            oMsNewProcess:SetRegua1(nRecCountStep)
            oTimeRemaining:=timeRemaining():New(nRecCountStep)

            oProgress:=txtProgress():New()

            TCSrvMap(cTmpAlias,nil,.F.)

            oFT:ft_fGoTop()
            while (!oFT:ft_fEof())

                nft_fRecNo:=oFT:ft_fRecNo()
                cLine:=oFT:ft_fReadLN()

                begin sequence

                    if (empty(cLine))
                        break
                    endif

                    aLine:=strTokArr2(cLine,";")

                    lAppend:=.T.

                    if ((lChkFilial).or.(lEnableFilterBlock))

                        for nField:=1 to nFields
                            nTargetFieldPos:=aTargetFieldsDef[nField][Len(aTargetFieldsDef[nField])]
                            if (nTargetFieldPos==0)
                                loop
                            endif
                            cField:=aTargetFieldsDef[nField][1]
                            if (((lChkFilial).and.(cField==cFieldFilial)).or.((lEnableFilterBlock).and.(cField$cFieldsFilterBlock)))
                                lHasError:=.F.
                                xValue:=(cTmpAlias)->(getTargetFieldValue(@lHasError,@oTFIni,@aLine,@cField,aTargetFieldsDef[nField],@aFieldsDef,@oHMSourceFieldsDef))
                                if (lHasError)
                                    cError:=CacheData():Get("EvalBlock","ErrorDescription","")
                                    cError+=CacheData():Get("EvalBlock","ErrorStack","")
                                    CacheData():delSection("EvalBlock")
                                    if (!empty(cError))
                                        cError:=cTools():StrReplace(cError,{CRLF,""})
                                        cTLogReport:="table : ["+cSourceFile+"] foram Encontradas inconsistencias na linha ["+cTools():NToS(nft_fRecNo)+"]"
                                        oTLogReport:AddGroup(cTLogReport)
                                        oTLogReport:AddDetail(cTLogReport,cError,nMLCountSize)
                                        lEnd:=oMsNewProcess:lEnd:=.T.
                                        lAppend:=.F.
                                        break
                                    endif
                                endif
                                if (empty(xValue))
                                    if (lHasError)
                                        cTLogReport:="table : ["+cSourceFile+"] foram Encontradas inconsistencias na linha ["+cTools():NToS(nft_fRecNo)+"]"
                                        oTLogReport:AddGroup(cTLogReport)
                                        oTLogReport:AddDetail(cTLogReport,"Estrutura da Linha diferente da estrutura de Dados")
                                        oTLogReport:AddDetail(cTLogReport,cLine,nMLCountSize)
                                    endif
                                    lAppend:=.F.
                                    exit
                                endif
                            endif
                            if ((lChkFilial).and.(cField==cFieldFilial))
                                SetFilAnt(xValue)
                            elseif ((lEnableFilterBlock).and.(cField$cFieldsFilterBlock))
                                lAppend:=(Eval(bEnableFilterBlockMem,xValue))
                                if (!lAppend)
                                    exit
                                endif
                            endif
                        next nField

                    endif

                    if (lEnd)
                        break
                    endif

                    if (lAppend)

                        if (lChkEncode)
                            cLineUTF8:=DecodeUTF8(cLine)
                            if (empty(cLineUTF8))
                                cLine:=OemToAnsi(cLine)
                                FWFreeArray(@aLine)
                                aLine:=strTokArr2(cLine,";")
                            endif
                            cLineUTF8:=""
                        endif

                        cLine:=""

                        (cTmpAlias)->(dbAppend(.F.))

                        ldbCommit:=(++nCommitBuffer>=1024)

                        if (lSeekTable)
                            cAllSeekIndexKey:=cSeekIndexKey
                        endif

                        for nField:=1 to nFields

                            nTargetFieldPos:=aTargetFieldsDef[nField][Len(aTargetFieldsDef[nField])]

                            if (nTargetFieldPos==0)
                                loop
                            endif

                            cField:=aTargetFieldsDef[nField][1]

                            if (lTypeReplicate)
                                nReplicates:=cacheData():Get("datasul2totvsprotheus",aTargetFieldsDef[nField][2]+"_TypeReplicate",0)
                                if (nReplicates>0)
                                    lTypeReplicate:=.F.
                                endif
                            endif

                            if ((!lChkFilial).and.(cField==cFieldFilial))

                                xValue:=cAliasxFilial

                            else

                                lHasError:=.F.
                                xValue:=(cTmpAlias)->(getTargetFieldValue(@lHasError,@oTFIni,@aLine,@cField,aTargetFieldsDef[nField],@aFieldsDef,@oHMSourceFieldsDef))

                                if (lHasError)

                                    cError:=CacheData():Get("EvalBlock","ErrorDescription","")
                                    cError+=CacheData():Get("EvalBlock","ErrorStack","")
                                    CacheData():delSection("EvalBlock")

                                    if (!empty(cError))
                                        cError:=cTools():StrReplace(cError,{CRLF,""})
                                        cTLogReport:="table : ["+cSourceFile+"] foram Encontradas inconsistencias na linha ["+cTools():NToS(nft_fRecNo)+"]"
                                        oTLogReport:AddGroup(cTLogReport)
                                        oTLogReport:AddDetail(cTLogReport,cError,nMLCountSize)
                                        lEnd:=oMsNewProcess:lEnd:=.T.
                                        lAppend:=.F.
                                        break
                                    endif //(!empty(cError))

                                endif //(lHasError)

                            endif //((!lChkFilial).and.(cField==cFieldFilial))

                            if (empty(xValue))
                                if (lHasError)
                                    cTLogReport:="table : ["+cSourceFile+"] foram Encontradas inconsistencias na linha ["+cTools():NToS(nft_fRecNo)+"]"
                                    oTLogReport:AddGroup(cTLogReport)
                                    oTLogReport:AddDetail(cTLogReport,"Estrutura da Linha diferente da estrutura de Dados")
                                    oTLogReport:AddDetail(cTLogReport,cLine,nMLCountSize)
                                    if ((cTmpAlias)->(Eval(bDeleteIfEmpty,cTmpAlias)))
                                        cDeleteIfEmpty+=(cTmpAlias)->(cTools():NToS(RecNo()))
                                        cDeleteIfEmpty+=","
                                        nDeleteIfEmpty++
                                        break
                                    endif
                                    exit
                                endif //(lHasError)
                                loop
                            endif //(empty(xValue))

                            (cTmpAlias)->(FieldPut(nTargetFieldPos,xValue))

                            if (lEnableFilterBlock)
                                if (cField$cFieldsFilterBlock)
                                    if ((cTmpAlias)->(!Eval(bEnableFilterBlock,cTmpAlias)))
                                        cDeleteIfEmpty+=(cTmpAlias)->(cTools():NToS(RecNo()))
                                        cDeleteIfEmpty+=","
                                        nDeleteIfEmpty++
                                        break
                                    endif
                                endif
                            endif

                            if ((lSeekTable).and.(!empty(cAllSeekIndexKey)))
                                if (cField$cAllSeekIndexKey)
                                    cAllSeekIndexKey:=cTools():StrReplace(cAllSeekIndexKey,{cField,""})
                                endif
                                if (empty(cTools():StrReplace(cAllSeekIndexKey,{"+",""})))
                                    cAllSeekIndexKey:=""
                                    (cSeekTable)->(MsSeek((cTmpAlias)->(&(cSeekIndexKey)),.F.))
                                endif
                            endif

                        next nField

                        if ((cTmpAlias)->(Eval(bDeleteIfEmpty,cTmpAlias)))
                            cDeleteIfEmpty+=(cTmpAlias)->(cTools():NToS(RecNo()))
                            cDeleteIfEmpty+=","
                            nDeleteIfEmpty++
                            break
                        endif

                        if (lEnableFilterBlock)
                            if ((cTmpAlias)->(!Eval(bEnableFilterBlock,cTmpAlias)))
                                cDeleteIfEmpty+=(cTmpAlias)->(cTools():NToS(RecNo()))
                                cDeleteIfEmpty+=","
                                nDeleteIfEmpty++
                                break
                            endif
                        endif

                        if (lSeekTable)

                            if (cSeekTable)->(MsSeek((cTmpAlias)->(&(cSeekIndexKey)),.F.))

                                if ((lUpdateTableFields).and.(cSeekTable)->(recLock(cSeekTable,.F.)))

                                    for nField:=1 to nUpdateTableSource

                                        cField:=aUpdateTableSource[nField]
                                        nTargetFieldPos:=aScan(aTargetFieldsDef,{|e|(e[1]==cField)})

                                        if (nTargetFieldPos>0)

                                            lHasError:=.F.
                                            xValue:=(cTmpAlias)->(getTargetFieldValue(@lHasError,@oTFIni,@aLine,@cField,aTargetFieldsDef[nTargetFieldPos],@aFieldsDef,@oHMSourceFieldsDef,@cType))

                                            if (lHasError)

                                                cError:=CacheData():Get("EvalBlock","ErrorDescription","")
                                                cError+=CacheData():Get("EvalBlock","ErrorStack","")
                                                CacheData():delSection("EvalBlock")

                                                if (!empty(cError))
                                                    cError:=cTools():StrReplace(cError,{CRLF,""})
                                                    cTLogReport:="table : ["+cSourceFile+"] foram Encontradas inconsistencias na linha ["+cTools():NToS(nft_fRecNo)+"]"
                                                    oTLogReport:AddGroup(cTLogReport)
                                                    oTLogReport:AddDetail(cTLogReport,cError,nMLCountSize)
                                                    lEnd:=oMsNewProcess:lEnd:=.T.
                                                    break
                                                endif //(!empty(cError))

                                            endif //(lHasError)

                                            cField:=aUpdateTableFields[nField]
                                            xValue:=NormalizeData(@lHasError,xValue,cType,cField,.T.,@st_oHMbTransform,@oTFIni)

                                            if (lHasError)

                                                cError:=CacheData():Get("EvalBlock","ErrorDescription","")
                                                cError+=CacheData():Get("EvalBlock","ErrorStack","")
                                                CacheData():delSection("EvalBlock")

                                                if (!empty(cError))
                                                    cError:=cTools():StrReplace(cError,{CRLF,""})
                                                    cTLogReport:="table : ["+cSourceFile+"] foram Encontradas inconsistencias na linha ["+cTools():NToS(nft_fRecNo)+"]"
                                                    oTLogReport:AddGroup(cTLogReport)
                                                    oTLogReport:AddDetail(cTLogReport,cError,nMLCountSize)
                                                    lEnd:=oMsNewProcess:lEnd:=.T.
                                                    (cSeekTable)->(MsUnLock())
                                                    break
                                                endif //(!empty(cError))

                                            endif //(lHasError)

                                        endif //(nTargetFieldPos>0)

                                        (cSeekTable)->(FieldPut(FieldPos(cField),xValue))

                                    next nField

                                    (cSeekTable)->(MsUnLock())

                                endif //((lUpdateTableFields).and.(cSeekTable)->(recLock(cSeekTable,.F.)))

                            endif //(cSeekTable)->(MsSeek((cTmpAlias)->(&(cSeekIndexKey)),.F.))

                        endif //(lSeekTable)

                        if (nReplicates>0)

                            aReplicateValues:=(cTmpAlias)->(aUtils():RegToArray(cTmpAlias,recNo(),aTargetReplicateFields,.F.,.F.,@aTargetReplicateFieldsPos))
                            nReplicate:=1

                            while (((++nReplicate)<=nReplicates))

                                (cTmpAlias)->(dbAppend(.F.))
                                ldbCommit:=(++nCommitBuffer>=1024)

                                for nField:=1 to nFields

                                    nTargetFieldPos:=aTargetFieldsDef[nField][Len(aTargetFieldsDef[nField])]

                                    if (nTargetFieldPos==0)
                                        loop
                                    endif

                                    cField:=aTargetFieldsDef[nField][1]

                                    if (cacheData():Get("datasul2totvsprotheus",aTargetFieldsDef[nField][2]+"_TypeReplicate",0)>0)

                                        aTargetFieldsDefReplicate:=aClone(aTargetFieldsDef[nField])
                                        aTargetFieldsDefReplicate[2]+=cTools():NToS(nReplicate)
                                        xValue:=(cTmpAlias)->(getTargetFieldValue(@lHasError,@oTFIni,@aLine,@cField,@aTargetFieldsDefReplicate,@aFieldsDef,@oHMSourceFieldsDef))

                                        FWFreeArray(@aTargetFieldsDefReplicate)

                                    else

                                        if (!oHMTargetReplicateFields:Get(cField,@nReplicateValuesAT))
                                            loop
                                        endif

                                        xValue:=aReplicateValues[nReplicateValuesAT]

                                    endif

                                    (cTmpAlias)->(FieldPut(nTargetFieldPos,xValue))

                                next nField

                                if ((cTmpAlias)->(Eval(bDeleteIfEmpty,cTmpAlias)))
                                    cDeleteIfEmpty+=(cTmpAlias)->(cTools():NToS(RecNo()))
                                    cDeleteIfEmpty+=","
                                    nDeleteIfEmpty++
                                endif

                            end while

                            FwFreeArray(@aReplicateValues)

                        endif

                    endif

                end sequence

                FWFreeArray(@aLine)

                nRecProc++
                if ((!lRecCountStep).or.(Mod(nRecProc,nIncProcStep)==0).or.(nRecProc>=nRecCount))
                    oTimeRemaining:Calcule(.T.)
                    cTimeRemaining:=" :: End of processing in ["+oTimeRemaining:GetcTRemaining()+"]["+DtoC(oTimeRemaining:GetdEndTime())+"]"
                    cTimeRemaining+="["+oTimeRemaining:GetcEndTime()+"]"
                    cTimeRemaining+="["+oTimeRemaining:GetcAverageTime()+"]"
                    cIncProc:="["+oProgress:Eval()+"] "
                    cIncProc+="Processing Record: "
                    cIncProc+=cTools():NToS(nft_fRecNo)
                    cIncProc+="/"
                    cIncProc+=cRecCount
                    cIncProc+=cTimeRemaining
                    cIncProc+="::["+cTools():NToS(if((nRecProc>=nRecCount),100,((nRecProc/nRecCount)*100)))+"%]"
                    oMsNewProcess:IncRegua1(cIncProc)
                endif

                lEnd:=oMsNewProcess:lEnd

                if (lEnd)
                    cTLogReport:="Process canceled by user"
                    oTLogReport:AddGroup(cTLogReport)
                    oTLogReport:AddDetail(cTLogReport,"Process canceled by user ["+DtoC(Date())+"]["+Time()+"]")
                    exit
                endif

                if (ldbCommit)
                    nCommitBuffer:=0
                    (cTmpAlias)->(dbCommitAll())
                endif

                if ((nDeleteIfEmpty>=1024).or.((ldbCommit).and.(nDeleteIfEmpty>0)))
                    if (!ldbCommit)
                        (cTmpAlias)->(dbCommitAll())
                    endif
                    if (right(cDeleteIfEmpty,1)==",")
                        cDeleteIfEmpty:=subStr(cDeleteIfEmpty,1,(Len(cDeleteIfEmpty)-1))
                    endif
                    TCSQLExec("DELETE FROM "+cFWTemporaryTableRealName+" WHERE R_E_C_N_O_ IN ("+cDeleteIfEmpty+")")
                    cDeleteIfEmpty:=""
                    nDeleteIfEmpty:=0
                endif

                if (ldbCommit)
                    ldbCommit:=.F.
                endif

                oFT:ft_fDelete()
                oFT:ft_fSkip()

            end while

            if (nCommitBuffer>0)
                (cTmpAlias)->(dbCommitAll())
            endif

            if (nDeleteIfEmpty>0)
                if (right(cDeleteIfEmpty,1)==",")
                    cDeleteIfEmpty:=subStr(cDeleteIfEmpty,1,(Len(cDeleteIfEmpty)-1))
                endif
                TCSQLExec("DELETE FROM "+cFWTemporaryTableRealName+" WHERE R_E_C_N_O_ IN ("+cDeleteIfEmpty+")")
                cDeleteIfEmpty:=""
                nDeleteIfEmpty:=0
            endif

            oFT:ft_fUse()

            dfv():Default(@lPreLoadTable,.F.)

            if ((lEnd).or.(lPreLoadTable))
                break
            endif

            nRecCount:=(cTmpAlias)->(recCount())
            cRecCount:=cTools():NToS(nRecCount)
            nRecCountStep:=if(nRecCount>=(nIncProcStep*2),Int(nRecCount/nIncProcStep),nRecCount)
            lRecCountStep:=(nRecCountStep!=nRecCount)
            if (lRecCountStep)
                nRecCountStep+=(nRecCount-(nRecCountStep*nIncProcStep))
            endif
            oMsNewProcess:SetRegua2(nRecCountStep)
            oTimeRemaining:SetRemaining(nRecCountStep)

            (cAlias)->(dbSetOrder(nIndexKey))

            nRecProc:=0

            lCustomUpdate:=((valType(bCustomUpdate)=="B").or.(allTrim(oTFIni:GetPropertyValue(cTableImport,"CustomUpdate","0"))=="1"))
            if (lCustomUpdate)
                if (valType(bCustomUpdate)!="B")
                    bCustomUpdate:=&(allTrim(oTFIni:GetPropertyValue(cTableImport,"bCustomUpdate","{|cTmpAlias,cAlias,lFound,lAddNew,cIndexKey,cIndexID,nFields,aTargetFieldsDef|.F.}")))
                endif
            endif
            lAppendIfNotExist:=(allTrim(oTFIni:GetPropertyValue(cTableImport,"AppendIfNotExist","1"))=="1")
            lUpdateEmptyValue:=(allTrim(oTFIni:GetPropertyValue(cTableImport,"UpdateEmptyValue","0"))=="1")
            bAfterUpdateRecNo:=&(allTrim(oTFIni:GetPropertyValue(cTableImport,"bAfterUpdateRecNo","{|cAlias,nRecNo|.F.}")))

            (cTmpAlias)->(dbGoTop())

            nTmpAliasOrder:=(cTmpAlias)->(IndexOrd())

            while (cTmpAlias)->(!eof())
                if ((cTmpAlias)->(!dbEval():GetNextRecNo(@cTmpAlias,@nTmpAliasNextRecNo,@nTmpAliasRecNo,@nTmpAliasOrder)))
				    exit
                endif
                cIndexID:=(cTmpAlias)->(&(cIndexKey))
                lFound:=(cAlias)->(dbSeek(cIndexID,.F.))
                begin sequence
                    if ((!lFound).and.(!lAppendIfNotExist))
                        break
                    endif
                    lAddNew:=(!lFound)
                    if (lCustomUpdate)
                        (cAlias)->(Eval(bCustomUpdate,cTmpAlias,cAlias,lFound,lAddNew,cIndexKey,cIndexID,nFields,aTargetFieldsDef))
                    else
                        if ((cAlias)->(recLock(cAlias,lAddNew)))
                            for nField:=1 to nFields
                                nTargetFieldPos:=aTargetFieldsDef[nField][Len(aTargetFieldsDef[nField])]
                                if (nTargetFieldPos==0)
                                    loop
                                endif
                                cField:=aTargetFieldsDef[nField][1]
                                if ((lFound).and.(cField$cIndexKey))
                                    loop
                                endif
                                xValue:=(cTmpAlias)->(FieldGet(nTargetFieldPos))
                                if (empty(xValue).and.(!lUpdateEmptyValue))
                                    loop
                                endif
                                if ((lMemoVirutal).and.(oHMMemoVirutal:Get(cField,@bUpdateMemoVirtual)))
                                    (cAlias)->(EvalBlock():EvalBlock(bUpdateMemoVirtual))
                                else
                                    (cAlias)->(FieldPut(nTargetFieldPos,xValue))
                                endif
                            next nField
                            if (valType(bAfterUpdateRecNo)=="B")
                                (cAlias)->(Eval(bAfterUpdateRecNo,cAlias,RecNo()))
                            endif
                            (cAlias)->(msUnLock())
                        endif
                    endif
                end sequence
                nRecProc++
                if ((!lRecCountStep).or.(Mod(nRecProc,nIncProcStep)==0).or.(nRecProc>=nRecCount))
                    oTimeRemaining:Calcule(.T.)
                    cTimeRemaining:=" :: End of processing in ["+oTimeRemaining:GetcTRemaining()+"]["+DtoC(oTimeRemaining:GetdEndTime())+"]"
                    cTimeRemaining+="["+oTimeRemaining:GetcEndTime()+"]"
                    cTimeRemaining+="["+oTimeRemaining:GetcAverageTime()+"]"
                    cIncProc:="["+oProgress:Eval()+"] "
                    cIncProc+="Updating Record: "
                    cIncProc+=cTools():NToS(nRecProc)
                    cIncProc+="/"
                    cIncProc+=cRecCount
                    cIncProc+=cTimeRemaining
                    cIncProc+="::["+cTools():NToS(if((nRecProc>=nRecCount),100,((nRecProc/nRecCount)*100)))+"%]"
                    oMsNewProcess:IncRegua2(cIncProc)
                endif
                lEnd:=oMsNewProcess:lEnd
                if (lEnd)
                    cTLogReport:="Process canceled by user"
                    oTLogReport:AddGroup(cTLogReport)
                    oTLogReport:AddDetail(cTLogReport,"Process canceled by user ["+DtoC(Date())+"]["+Time()+"]")
                    exit
                endif
                if ((cTmpAlias)->(!dbEval():GoToNextRecNo(@cTmpAlias,@nTmpAliasNextRecNo,@nTmpAliasOrder)))
                    exit
                endif
            end while

            if (lEnd)
                break
            endif

        end sequence

        if (!lPreLoadTable)
            (cTmpAlias)->(dbCloseArea())
            oFWTemporaryTable:Delete()
            oFWTemporaryTable:=FreeObj(oFWTemporaryTable)
        endif

    end sequence

    SetFilAnt(cSVFilAnt)

    CacheData():delSection("EvalBlock")

    if (valtype(oHMSourceFieldsDef)=="O")
        oHMSourceFieldsDef:Clean()
        oHMSourceFieldsDef:=FreeObj(oHMSourceFieldsDef)
    endif

    if (lMemoVirutal)
        if (valtype(oHMMemoVirutal)=="O")
            oHMMemoVirutal:Clean()
            oHMMemoVirutal:=FreeObj(oHMMemoVirutal)
        endif
    endif

    if (lTypeReplicate)
        FWFreeArray(@aTargetReplicateFields)
        FWFreeArray(@aTargetReplicateFieldsPos)
        if (valtype(oHMTargetReplicateFields)=="O")
            oHMTargetReplicateFields:Clean()
            oHMTargetReplicateFields:=FreeObj(oHMTargetReplicateFields)
        endif
    endif

    FWFreeArray(@aTargetStruct)
    FWFreeArray(@aFieldsDef)
    FWFreeArray(@aTargetFieldsDef)

    DelClassIntF()

return(lImportData)
