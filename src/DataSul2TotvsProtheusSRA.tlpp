#include "totvs.ch"

#include "execin.th"
EXECIN CLASS SRAImport

static function SRAGetValue(xGet as variant,xDefault as variant) as variant
return(cacheData():Get("datasul2totvsprotheus",xGet,xDefault))

static function SRASetValue(xVar as variant,xValue as variant) as variant
return(cacheData():Set("datasul2totvsprotheus",xVar,xValue))

static function SRAImportData(oMsNewProcess as object,lEnd as logical,lPercentage as logical) as logical

    local aAddIndex as array

    local bSourceSetFilter as codeblock

    local cAlias:="SRA" as character
    local cIndexKey as character
    local cTableImport:="SRA" as character

    local lSRAImport as logical

    local oTFIni:=cacheData():Get("datasul2totvsprotheus","readIniFile") as object
    local oTLogReport:=tLogReport():New() as object

    cIndexKey:=allTrim(oTFIni:GetPropertyValue(cTableImport,"IndexKey","RA_FILIAL+RA_MAT"))
    aAddIndex:=strToKArr2(allTrim(oTFIni:GetPropertyValue(cTableImport,"AddIndex","RA_FILIAL,RA_MAT")),",")

    bSourceSetFilter:={|cLine|SRASourceFilter(cLine,oTFIni)}
    cacheData():Set("datasul2totvsprotheus",cTableImport+"SourceSetFilter",bSourceSetFilter)

    lSRAImport:=datasul2totvs.datasul2totvs():Execute("datasul2totvs.ImportData",@oMsNewProcess,@lEnd,lPercentage,@oTLogReport,@cAlias,@cIndexKey,@cTableImport,@aAddIndex)

    if ((lSRAImport).and.(!lEnd))
        oMsNewProcess:SetRegua1(0)
        oMsNewProcess:IncRegua1("Wait!")
        oMsNewProcess:SetRegua2(0)
        oMsNewProcess:IncRegua2("...")
        cTableImport:="SRA_RHPF"
        cIndexKey:=allTrim(oTFIni:GetPropertyValue(cTableImport,"IndexKey","RA_MATMIG"))
        FWFreeArray(@aAddIndex)
        aAddIndex:=strToKArr2(allTrim(oTFIni:GetPropertyValue(cTableImport,"AddIndex","RA_MATMIG")),",")
        lSRAImport:=datasul2totvs.datasul2totvs():Execute("datasul2totvs.ImportData",@oMsNewProcess,@lEnd,lPercentage,@oTLogReport,@cAlias,@cIndexKey,@cTableImport,@aAddIndex)
        if ((lSRAImport).and.(!lEnd))
            oMsNewProcess:SetRegua1(0)
            oMsNewProcess:IncRegua1("Wait!")
            oMsNewProcess:SetRegua2(0)
            oMsNewProcess:IncRegua2("...")
            cTableImport:="SRA_SPED"
            cIndexKey:=allTrim(oTFIni:GetPropertyValue(cTableImport,"IndexKey","RA_FILIAL+RA_MAT"))
            FWFreeArray(@aAddIndex)
            aAddIndex:=strToKArr2(allTrim(oTFIni:GetPropertyValue(cTableImport,"AddIndex","RA_FILIAL,RA_MAT")),",")
            cacheData():Set("datasul2totvsprotheus",cTableImport+"SourceSetFilter",bSourceSetFilter)
            lSRAImport:=datasul2totvs.datasul2totvs():Execute("datasul2totvs.ImportData",@oMsNewProcess,@lEnd,lPercentage,@oTLogReport,@cAlias,@cIndexKey,@cTableImport,@aAddIndex)
            if ((lSRAImport).and.(!lEnd))
                ApMsgInfo(cAlias+" Table Import Completed","Info")
            elseif (!lEnd)
                ApMsgInfo("Problems were encountered while processing the "+cTableImport+" table","Info")
            endif            
        elseif (!lEnd)
            ApMsgInfo("Problems were encountered while processing the "+cTableImport+" table","Info")
        endif
    endif

    oTLogReport:PrintDialog("Log de Ocorrencias na Importacao: ["+cAlias+"]"+Upper(FWSX2Util():GetX2Name(cAlias,.F.)))
    MsAguarde({||oTLogReport:Clean()},"Releasing temporary files and workspace memory...","Wait...")
    oTLogReport:=FreeObj(oTLogReport)

    FWFreeArray(@aAddIndex)

    DelClassIntF()

return(lSRAImport)

static function SRASourceFilter(cLine as character,oTFIni as object) as logical

    local aLine as array

    local nAT as numeric

    local lFilter:=.T. as logical

    local xFilter as variant

    begin sequence
        if (empty(cLine))
            break
        endif
        cLine:=subStr(cLine,1,15)
        nAT:=RAT(";",cLine)
        if (nAT>0)
            cLine:=substr(cLine,1,(nAT-1))
        endif
        aLine:=strToKArr2(cLine,";")
        if (empty(aLine).or.(Len(aLine)<2))
            break
        endif
        xFilter:=strTran(aLine[1],'"',"")
        xFilter:=datasul2totvs.datasul2totvs():Execute("datasul2totvs.FindInTable",oTFIni,"TabelaEmpresas",xFilter,.T.,.T.)
        lFilter:=(xFilter==cEmpAnt)
        if (!lFilter)
            break
        endif
        xFilter:=aLine[2]
        xFilter:=strTran(aLine[2],'"',"")
        xFilter:=datasul2totvs.datasul2totvs():Execute("datasul2totvs.FindInTable",oTFIni,"TabelaFiliais",xFilter,.T.,.T.)
        lFilter:=(!empty(xFilter))
        if (!lFilter)
            break
        endif
    end sequence

    FWFreeArray(@aLine)

return(lFilter)
