#include "totvs.ch"

#include "execin.th"
EXECIN CLASS SRDImport

static function SRDImportData(oMsNewProcess as object,lEnd as logical) as logical

    local aAddIndex as array

    local bEnableFilterBlock as codeblock
    local bEnableFilterBlockMem as codeblock

    local cAlias:="SRD" as character
    local cIndexKey as character
    local cTableImport:="SRD" as character

    local cCompetenciaDe as character
    local cCompetenciaAte as character

    local lSRDImport as logical
    local lEnableFilterBlock as logical

    local oTFIni:=cacheData():Get("datasul2totvsprotheus","readIniFile") as object
    local oTLogReport:=tLogReport():New() as object
    local oSRDPergunte:=tHash():New() as object

    begin sequence

        lEnableFilterBlock:=(allTrim(oTFIni:GetPropertyValue(cAlias,"EnableFilterBlock","0"))=="1")
        if (lEnableFilterBlock)
            if (!SRDPergunte(@oSRDPergunte))
                break
            endif
            cCompetenciaDe:=oSRDPergunte:Get("Competencia.De")
            cCompetenciaAte:=oSRDPergunte:Get("Competencia.Ate")
            bEnableFilterBlock:={|cAlias|(cAlias)->((RD_DATARQ>=cCompetenciaDe).and.(RD_DATARQ<=cCompetenciaAte))}
            bEnableFilterBlockMem:={|xValue,cField|;
                IF((cField=="RD_DATARQ"),;
                    ((xValue>=cCompetenciaDe).and.(xValue<=cCompetenciaAte)),;
                    IF((cField=="RD_PD"),;
                        (!empty(xValue).and.(PosSRV(xValue,fFilFunc("SRA"),"RV_COD",1,.T.)==xValue)),;
                        .T.;
                    );
                );
            }
            cacheData():Set("datasul2totvsprotheus",cAlias+"EnableFilterBlock",bEnableFilterBlock)
            cacheData():Set("datasul2totvsprotheus",cAlias+"EnableFilterBlockMem",bEnableFilterBlockMem)
        else
            cacheData():DelProperty("datasul2totvsprotheus",cAlias+"EnableFilterBlock")
            cacheData():DelProperty("datasul2totvsprotheus",cAlias+"EnableFilterBlockMem")
        endif

        cIndexKey:=allTrim(oTFIni:GetPropertyValue(cAlias,"IndexKey","RD_FILIAL+RD_MAT+RD_CC+RD_ITEM+RD_CLVL+RD_DATARQ+RD_PD+RD_SEQ+RD_PERIODO+RD_SEMANA+RD_ROTEIR+DTOS(RD_DTREF)+RD_CONVOC+RD_NRBEN"))
        aAddIndex:=strToKArr2(allTrim(oTFIni:GetPropertyValue(cAlias,"AddIndex","RD_FILIAL,RD_MAT,RD_CC,RD_ITEM,RD_CLVL,RD_DATARQ,RD_PD,RD_SEQ,RD_PERIODO,RD_SEMANA,RD_ROTEIR,RD_DTREF,RD_CONVOC,RD_NRBEN")),",")

        cacheData():Set("datasul2totvsprotheus","SRDImportDataLogReport",oTLogReport)

        lSRDImport:=datasul2totvs.Protheus():ImportData(@oMsNewProcess,@lEnd,@oTLogReport,@cAlias,@cIndexKey,@cTableImport,@aAddIndex)

        if (lSRDImport)
            ApMsgInfo(cAlias+" Table Import Completed","Info")
        else
            ApMsgInfo("Problems were encountered while processing the "+cTableImport+" table","Info")
        endif

        oTLogReport:PrintDialog("Log de Ocorrencias na Importacao: ["+cAlias+"]"+Upper(FWSX2Util():GetX2Name(cAlias,.F.)))
        oTLogReport:Clean()
        oTLogReport:=FreeObj(oTLogReport)

        cacheData():DelProperty("datasul2totvsprotheus",cAlias+"LogReport")

        FWFreeArray(@aAddIndex)

    end sequence

    if (findFunction("RstPosAlias"))
        RstPosAlias()
    endif

    if (valtype(oSRDPergunte)=="O")
        oSRDPergunte:Clear()
        oSRDPergunte:=FreeObj(oSRDPergunte)
    endif

    if (findFunction("RstPosAlias"))
        RstPosAlias()
    endif

    if (type("oPVTHMSRDSeq")=="O")
        oPVTHMSRDSeq:Clean()
        oPVTHMSRDSeq:=FreeObj(oPVTHMSRDSeq)
    endif

    DelClassIntF()

return(lSRDImport)

static function SRDPergunte(oSRDPergunte as object) as logical

    local aPBoxPrm:=Array(0) as array
    local aPBoxRet:=Array(0) as array

    local cPBoxTit:=OemToAnsi("Informe os parametros") as character

    local cDatArq as character

    local lParamBox:=.F. as logical

    local nPBox as numeric
    local nDatArq as numeric

    saveInter()

        nDatArq:=GetSx3Cache("RD_DATARQ","X3_TAMANHO")
        cDatArq:=Space(nDatArq)

        aAdd(aPBoxPrm,Array(9))
        nPBox:=Len(aPBoxPrm)
        //01----------------------------------------------------------------------------------------------
        aPBoxPrm[nPBox][1]:=1               //[1]:1 - MsGet
        aPBoxPrm[nPBox][2]:="Competencia.De"//[2]:Descricao
        aPBoxPrm[nPBox][3]:=cDatArq  //[3]:String contendo o inicializador do campo
        aPBoxPrm[nPBox][4]:="@R 9999/99"    //[4]:String contendo a Picture do campo
        aPBoxPrm[nPBox][5]:="NaoVazio()"    //[5]:String contendo a validacao
        aPBoxPrm[nPBox][6]:=""              //[6]:Consulta F3
        aPBoxPrm[nPBox][7]:="AllWaysTrue()" //[7]:String contendo a validacao When
        aPBoxPrm[nPBox][8]:=CalcFieldSize("C",nDatArq,0,aPBoxPrm[nPBox][4],aPBoxPrm[nPBox][2]) //[8]:Tamanho do MsGet
        aPBoxPrm[nPBox][9]:=.T.             //[9]:Flag .T./.F. Parametro Obrigatorio ?

        aAdd(aPBoxPrm,Array(9))
        nPBox:=Len(aPBoxPrm)
        //01----------------------------------------------------------------------------------------------
        aPBoxPrm[nPBox][1]:=1                //[1]:1 - MsGet
        aPBoxPrm[nPBox][2]:="Competencia.Ate"//[2]:Descricao
        aPBoxPrm[nPBox][3]:=cDatArq   //[3]:String contendo o inicializador do campo
        aPBoxPrm[nPBox][4]:="@R 9999/99"     //[4]:String contendo a Picture do campo
        aPBoxPrm[nPBox][5]:="NaoVazio()"     //[5]:String contendo a validacao
        aPBoxPrm[nPBox][6]:=""               //[6]:Consulta F3
        aPBoxPrm[nPBox][7]:="AllWaysTrue()"  //[7]:String contendo a validacao When
        aPBoxPrm[nPBox][8]:=CalcFieldSize("C",nDatArq,0,aPBoxPrm[nPBox][4],aPBoxPrm[nPBox][2])  //[8]:Tamanho do MsGet
        aPBoxPrm[nPBox][9]:=.T.              //[9]:Flag .T./.F. Parametro Obrigatorio ?

        while (!(lParamBox:=ParamBox(@aPBoxPrm,@cPBoxTit,@aPBoxRet,NIL,NIL,.T.,NIL,NIL,NIL,NIL,.T.,.T.)))
            lParamBox:=MsgYesNo("Deseja Abortar a Importacao?","Atencao!")
            if (lParamBox)
                lParamBox:=.F.
                exit
            endif
        end while

        if (lParamBox)
            for nPBox:=1 To Len(aPBoxPrm)
                oSRDPergunte:Set(aPBoxPrm[nPBox][2],aPBoxRet[nPBox])
            next nPBox
        endif

    restInter()

    FWFreeArray(@aPBoxRet)
    FWFreeArray(@aPBoxPrm)

return(lParamBox)

static function SRDGetValue(xGet,xDefault) as variant
return(cacheData():Get("datasul2totvsprotheus",xGet,xDefault))

static function SRDGetSEQ() as character

    static c_stHMSRDSeqKey as character

    local cStack:="IMPORTDATA" as character

    local cRDSEQ:="" as character

    local cHMSRDSeqKey:="" as character

    local cTmpAlias as character

    begin sequence

        if (type("cPVTSRDAlias")!="C")
            _SetNamedPRVT("cPVTSRDAlias",SRDGetValue("importSRDAlias","__NOALIAS__"),cStack)
        endif

        cTmpAlias:=cPVTSRDAlias
        if (select(cTmpAlias)==0)
            break
        endif

        cHMSRDSeqKey:=&("cEmpAnt")
        cHMSRDSeqKey+=SRDGetValue("RD_FILIAL")
        cHMSRDSeqKey+=SRDGetValue("RD_MAT")
        cHMSRDSeqKey+=SRDGetValue("RD_DATARQ")
        cHMSRDSeqKey+=SRDGetValue("RD_PD")
        cHMSRDSeqKey+=SRDGetValue("RD_SEMANA")
        cHMSRDSeqKey+=SRDGetValue("RD_ROTEIR")
        cHMSRDSeqKey+=SRDGetValue("RD_CC")
        cHMSRDSeqKey+=SRDGetValue("RD_PROCES")

        if (type("oPVTHMSRDSeq")!="O")
            _SetNamedPRVT("oPVTHMSRDSeq",THashMap():New(),cStack)
        else
            if (c_stHMSRDSeqKey!=cHMSRDSeqKey)
                c_stHMSRDSeqKey:=cHMSRDSeqKey
                oPVTHMSRDSeq:Clean()
            endif
        endif

        if (!oPVTHMSRDSeq:Get(cHMSRDSeqKey,@cRDSEQ)).or.(Empty(cRDSEQ))
            if (type("nPVTSRDSeq")!="N")
                _SetNamedPRVT("nPVTSRDSeq",GetSx3Cache("RD_SEQ","X3_TAMANHO"),cStack)
                cRDSEQ:=Replicate("0",nPVTSRDSeq)
                _SetNamedPRVT("cPVTRDSEQ",cRDSEQ,cStack)
            endif
            cRDSEQ:=cPVTRDSEQ
        endif

        cRDSEQ:=__Soma1(cRDSEQ)

        oPVTHMSRDSeq:Set(cHMSRDSeqKey,cRDSEQ)

    end sequence

return(cRDSEQ)

static function SRDGetSRVPD(cEventoDataSul as character,oTFIni as object) as character

    local cRDPD as character
    local cSRVFilial as character
    local cSRVKeySeek as character

    local cTReportGrp as character
    local cTReportMsg as character

    local lSRVFound as logical

    local nSRDPD:=getSX3Cache("RD_PD","X3_TAMANHO")
    local nSRVOrder as numeric

    local oTLogReport as object 

    cRDPD:=datasul2totvs.findInTable(oTFIni,"TabelaVerbasSRV",cEventoDataSul,.T.)
    cRDPD:=Left(cRDPD,nSRDPD)

    if (!Empty(cRDPD))

        cSRVFilial:=fFilFunc("SRV")
        
        cSRVKeySeek:=cSRVFilial
        cSRVKeySeek+=cRDPD
        
        nSRVOrder:=RetOrder("SRV","RV_FILIAL+RV_COD")

        SRV->(dbSetOrder(nSRVOrder))
        
        lSRVFound:=SRV->(MsSeek(cSRVKeySeek,.F.))
        
        if (!lSRVFound)
            oTLogReport:=SRDGetValue("SRDImportDataLogReport",nil)
            if (valType(oTLogReport)=="O")
                cTReportGrp:="Verba Não Localizada"
                cTReportMsg:="Verba ["+cRDPD+"] Não Localizada no SRV."
                oTLogReport:AddGroup(cTReportGrp)
                oTLogReport:AddDetail(cTReportGrp,cTReportMsg)
            endif
            cRDPD:=Space(nSRDPD)
        endif
    
    else

        oTLogReport:=SRDGetValue("SRDImportDataLogReport",nil)
        if (valType(oTLogReport)=="O")
            cTReportGrp:="["+cEventoDataSul+"] De/Para Não Localizado"
            cTReportMsg:="Não Existe paridade de Verba Protheus para o Evento Datasul ["+cEventoDataSul+"]."
            oTLogReport:AddGroup(cTReportGrp)
            oTLogReport:AddDetail(cTReportGrp,cTReportMsg)
        endif

    endif

return(cRDPD)
